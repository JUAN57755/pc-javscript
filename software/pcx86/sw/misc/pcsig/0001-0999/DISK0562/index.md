---
layout: page
title: "PC-SIG Diskette Library (Disk #562)"
permalink: /software/pcx86/sw/misc/pcsig/0001-0999/DISK0562/
machines:
  - id: ibm5170
    type: pcx86
    config: /machines/pcx86/ibm/5170/cga/1024kb/rev3/machine.xml
    diskettes: /machines/pcx86/diskettes.json,/disks/pcsigdisks/pcx86/diskettes.json
    autoGen: true
    autoMount:
      B: "PC-SIG Library Disk #0562"
    autoType: $date\r$time\rB:\rDIR\r
---

{% include machine.html id="ibm5170" %}

{% comment %}info_begin{% endcomment %}

## Information about "PC-HAM"

    PC-HAM is a set of amateur radio database programs.  The program is
    based, in part, on programs described in, "Software for Amateur
    Radio," by Joe Kasser (G3ZCZ) and published by TAB Books.
    
    PC-HAM has several features that are invaluable to any amateur
    radio operator with a computer. Some of the program's capabilities
    include:
    
    ~ Display/print of your QSO's sorted by call-sign, alphanumerical order
    
    ~ Search for and display of all QSO's with a specified prefix
    
    ~ Automatic generation of your DXCC status
    
    ~ Direct updating of award records from the log entries
    
    ~ Analyze contest QSO's for duplicates and scoring contacts
    
    ~ Automatic QSL 100% (after a contest or DX-Pedition) to all stations
    worked for one contact on each of five bands.
{% comment %}info_end{% endcomment %}

{% comment %}samples_begin{% endcomment %}

## CONTEST.BAS

```bas
10 ON ERROR GOTO 1200
20 KEY OFF:G$="G3ZCZ CONTEST LOGGING PROGRAM VERSION 3.0" :CLS:WIDTH 80:LOCATE 5,1:PRINT G$:LOCATE 10,1:PRINT "IF TIME IS NOT SET TO UTC, STOP NOW AND SET THE CLOCK IN DOS"
30 K1=0:GOSUB 930
40 BANDS=7:M9=4000:DIM W$(M9),F1(M9),B(BANDS),B1(BANDS):R1$="00":S1$="599":C$="?"
50 BLANK$="                                                                    "
60 C4$="RXWBMCFL*/Q?OP+":L0$=BLANK$:QS$="-":QR$=QS$:R$=R1$:S$=S1$:L1$=L0$:L2$=L0$:L3$=L0$:L4$=L0$
70 L5$=L0$:L6$=L0$:L7$=L0$:L8$=L0$:L13$=L0$:L12$=L0$:L11$=L0$:L10$=L0$:L9$=L0$
80 FOR I%=1 TO BANDS:READ B(I%):NEXT:DATA 1,4,16,64,256,1024,4096
90 FOR I%=1 TO BANDS:READ B1(I%):NEXT:DATA 10,15,20,40,80,160,2
100 LOCATE 15,1:INPUT "Enter name of lOG file (D:NAME) Log type .LOG Assumed ",L$:IF L$="" THEN 100
110 GOSUB 880
120 CLS
130 OPEN L$+".$$$" FOR OUTPUT AS #2
140 OPEN L$+".LOG" FOR INPUT AS #1:GOTO 160
150 GOSUB 420 :GOSUB 430 :GOSUB 460:GOTO 1000
160 LOCATE 23,1:PRINT "LOADING LAST ENTRY STRINGS":OPEN L$+".CHK" FOR INPUT AS #3
170 INPUT#3,L13$,L12$,L11$,L10$,L9$,L8$,L7$,L6$,L5$,L4$,L3$,L2$,L1$:CLOSE#3
180 PRINT "OPENING LOGBOOK ";L$:N4=0
190 IF EOF(1) THEN INPUT"Waiting,touch ENTER to continue ";A$:GOSUB 470:GOTO 1000
200 INPUT#1,D$,T$,B$,C$,R$,S$,M$,P$,QS$,QR$,X$ : PRINT N4,C$,X$:IF C$="/*" THEN N4=N4-1 ELSE N4=N4+1
210 IF C$="/*" THEN C$=C8$:GOSUB 300:W$(J)=" ":F1(J)=F1(J)-B(B%):C$="/*":GOTO 240
220 GOSUB 300:W$(J)=C$:F=VAL(B$):FOR Q%=1 TO BANDS:IF INT(F)=B1(Q%) THEN F1(J)=F1(J)+B(Q%):B%=Q%:GOTO 240
230 NEXT
240 C8$=C$:WRITE#2,D$;T$;B$;C$;R$;S$;M$;P$;QS$;QR$;X$:GOTO 190
250 LOCATE 20,1:PRINT BLANK$:LOCATE 20,1:RETURN
260 L0$=BLANK$:MID$(L0$,6)=D$:MID$(L0$,15)=T$:MID$(L0$,20)=C$:MID$(L0$,32)=B$:MID$(L0$,36)=M$:MID$(L0$,40)=P$
270 IF C$="/*" THEN MID$(L0$,1)="-" ELSE N4$=STR$(N4):MID$(L0$,1)=MID$(N4$,2)
280 MID$(L0$,45)=R$:MID$(L0$,49)=S$:MID$(L0$,53)=QS$:MID$(L0$,55)=QR$:MID$(L0$,57)=X$:RETURN
290 T$=LEFT$(TIME$,2)+MID$(TIME$,4,2):D$=RIGHT$(DATE$,2)+"/"+LEFT$(DATE$,2)+"/"+MID$(DATE$,4,2):RETURN
300 J=0:IF C$="/*" THEN 380
310 FOR I%=1 TO LEN(C$):A$=MID$(C$,I%,1):J=J+(ASC(A$)-47):NEXT:J=J*ASC(LEFT$(C$,1)):IF J<0 THEN J=0
320 IF J>M9 THEN J = J-M9 : GOTO 320
330 IF LEN(W$(J))<1 THEN 380
340 IF LEFT$(W$(J),1)=" " THEN 380
350 IF LEFT$(W$(J),LEN(C$))=C$ THEN 370
360 J=J+1:GOTO 320
370 I=0:RETURN
380 I=1:RETURN
390 INPUT "REPORT RECEIVED ";A$:IF A$="" THEN 400 ELSE X$=A$
400 RETURN
410 GOSUB 250:INPUT "REPORT SENT     ";S$:IF S$>"599" THEN 410 ELSE RETURN
420 GOSUB 250:INPUT "POWER (watts)   ";P$:IF P$<"0" THEN 420 ELSE RETURN
430 GOSUB 250:INPUT "BAND            ";F : IF F>160 THEN 430
440 FOR I%=1 TO BANDS:IF INT(F)=B1(I%) THEN 450 ELSE NEXT:LOCATE 22,1:FOR I%=1 TO BANDS:PRINT B1(I%);" ";:NEXT:GOTO 430
450 B$=MID$(STR$(F),2):B%=I%:GOSUB 730:RETURN
460 GOSUB 250:INPUT "MODE            ";M$: IF M$="" THEN 460
470 IF M$="SSB" THEN S1$="59" ELSE S1$="599"
480 RETURN
490 GOSUB 250:INPUT "CALL SIGN       ";A$
500 IF A$="" THEN 580 :IF LEFT$(A$,1)="" THEN A$=LEFT$(A$,LEN(A$)):GOTO 500
510 LOCATE 22,1:PRINT BLANK$: LOCATE 22,1:C$=A$:GOSUB 300:IF I=0 THEN 530
520 PRINT "OK":GOTO 580
530 F2=F1(J): PRINT "WORKED on ";
540 FOR Q%=BANDS TO 1 STEP -1: IF F2<B(Q%) THEN 570
550 PRINT B1(Q%);" ";:F2=F2-B(Q%):IF B1(Q%)=INT(F) THEN PRINT "DUPLICATE";CHR$(7);
560 IF F2>=B(Q%) THEN 540
570 NEXT
580 IF P2=1 THEN LPRINT C$
590 RETURN
600 GOSUB 250:INPUT "ARE YOU SURE ";A$ :IF A$="" THEN RETURN
610 IF N4<1 THEN GOSUB 250 : PRINT "CAN'T F***** A ZERO ENTRY":RETURN
620 IF A$=""OR LEFT$(A$,1) <> "Y" THEN RETURN
630 C$=C8$:GOSUB 300:F1(J)=F1(J)-B(B%):IF F1(J)<=0 THEN W$(J)=" "
640 C$="/*":GOTO 680
650 IF LEN(X$)=0 THEN 660 ELSE IF LEFT$(C$,1)="?" THEN 660 ELSE 670
660 LOCATE 22,1:PRINT BLANK$: LOCATE 22,1:PRINT "GET THE DATA FIRST":RETURN
670 GOSUB 290:C8$=C$:GOSUB 300: F1(J)=F1(J)+B(B%):W$(J)=C$:GOSUB 1070
680 WRITE#2,D$;T$;B$;C$;R$;S$;M$;P$;QS$;QR$;X$
690 IF C$="/*" THEN 700 ELSE N4=N4+1:GOTO 710
700 N4=N4-1:IF N4<0 THEN N4=0
710 GOSUB 260:L13$=L12$:L12$=L11$:L11$=L10$:L10$=L9$:LOCATE 1,5:PRINT G$
720 L9$=L8$:L8$=L7$:L7$=L6$:L6$=L5$:L5$=L4$:L4$=L3$:L3$=L2$:L2$=L1$:L1$=L0$:IF P1 = 1 THEN LPRINT L0$
730 GOSUB 740:C$="?":R$=R1$:S$=S1$:X$="":RETURN
740 I=3:GOSUB 780:PRINT L13$:I=4:GOSUB 780:PRINT L12$:I=5:GOSUB 780:PRINT L11$:I=6:GOSUB 780:PRINT L10$
750 I=7:GOSUB 780:PRINT L9$:I=8:GOSUB 780:PRINT L8$:I=9:GOSUB 780:PRINT L7$:I=10:GOSUB 780:PRINT L6$
760 I=11:GOSUB 780:PRINT L5$:I=12:GOSUB 780:PRINT L4$:I=13:GOSUB 780:PRINT L3$:I=14:GOSUB 780:PRINT L2$
770 I=15:GOSUB 780:PRINT L1$:RETURN
780 LOCATE I,1:PRINT BLANK$:LOCATE I,1:RETURN
790 GOSUB 250:INPUT "ARE YOU SURE ";A$:IF A$="" THEN RETURN
800 IF LEFT$(A$,1)<>"Y" THEN RETURN
810 CLS:PRINT "SAVING LAST ENTRY STRINGS BEFORE CLOSING":OPEN L$+".CHK" FOR OUTPUT AS #3
820 WRITE#3,L13$;L12$;L11$;L10$;L9$;L8$;L7$;L6$;L5$;L4$;L3$;L2$;L1$
830 CLOSE#3:CLOSE#1:CLOSE#2
840 KILL L$+".BAK"
850 NAME L$+".LOG" AS L$+".BAK"
860 NAME L$+".$$$" AS L$+".LOG"
870 GOTO 1350 : REM END
880 INPUT "Do you want a running log printed out (Y/N) "; A$: IF A$="" THEN 880
890 IF LEFT$(A$,1)="Y" THEN P1 = 1 ELSE IF LEFT$(A$,1)="N" THEN P1 = 0 ELSE 880
900 INPUT "Do you want CALL SIGNS printed out (Y/N) "; A$: IF A$="" THEN 900
910 IF LEFT$(A$,1)="Y" THEN P2 = 1 ELSE IF LEFT$(A$,1)="N" THEN P2 = 0 ELSE 900
920 RETURN
930 IF K1 = 1 THEN 970
940 KEY 1,"QSL   "+CHR$(13): KEY 2,"BAND  "+CHR$(13):KEY 3,"CALL  "+CHR$(13):KEY 4, "*QRT  "+CHR$(13):KEY 5,"RPT RX "+CHR$(13)
950 KEY 6,"FUDGE "+CHR$(13): KEY 7,"XMT RPT"+CHR$(13):KEY 8,"/PX CHK"+CHR$(13):KEY 9, "LOG   "+CHR$(13):KEY 10,"+MORE "+CHR$(13):K1=1
960 GOTO 990
970 KEY 1,"WATTS "+CHR$(13): KEY 2,"MODE  "+CHR$(13):KEY 3,"?CLN  "+CHR$(13):KEY 4, "PRINT "+CHR$(13):KEY 5,"OOPS  "+CHR$(13)
980 KEY 6,"      "+CHR$(13): KEY 7,"        "+CHR$(13):KEY 8,"        "+CHR$(13):KEY 9, "      "+CHR$(13):KEY 10,"+MORE "+CHR$(13):K1=0
990 RETURN
1000 GOSUB 1090: GOSUB 730
1010 GOSUB 1100
1020  LOCATE 17,30:PRINT "CURRENT ENTRY":GOSUB 290:GOSUB 260
1030 MID$(L0$,1)=STR$(N4+1):LOCATE 18,1 : PRINT BLANK$:LOCATE 18,1 : PRINT STR$(N4+1),C$,X$
1040 GOSUB 250:INPUT "QRU ";A$:IF A$=""THEN 1040
1050 FOR J2%=1 TO LEN(C4$):IF LEFT$(A$,1)=MID$(C4$,J2%,1) THEN 1060 ELSE NEXT:GOTO 1040
1060 GOSUB 250:ON J2% GOSUB 390,410,420,430,460,490,600,650,790,1130,1080,1070,1370,880,930:GOTO 1010
1070 LOCATE 21,1:PRINT BLANK$:PRINT BLANK$:PRINT BLANK$:RETURN
1080 LOCATE 22,1:PRINT L0$:RETURN
1090 CLS: KEY ON: RETURN
1100 LOCATE 2,1:PRINT"-------------------------------------------------------------------------------"
1110 LOCATE 1,5:PRINT G$,L$
1120 LOCATE 16,1:PRINT"-------------------------------------------------------------------------------":RETURN
1130 GOSUB 250:INPUT "ARE YOU SURE ";A$:IF A$="" THEN RETURN
1140 INPUT "WHICH PREFIX ";A$:IF LEN(A$)<1 THEN A$="*"
1150 CLS:GOSUB 1100:LOCATE 3,1
1160 I%=0:FOR Q=1 TO M9:IF LEN(W$(Q))<=2 THEN 1190
1170 IF A$="*" THEN 1180 ELSE IF A$=LEFT$(W$(Q),LEN(A$)) THEN 1180 ELSE 1190
1180 PRINT W$(Q),:I%=I%+1:IF I%=4 THEN I%=0:PRINT
1190 NEXT:PRINT:INPUT "READY WHEN YOU ARE, HIT ENTER TO CONTINUE";A$:CLS:GOSUB 1090:GOSUB 740:RETURN
1200 IF ERR = 64 OR ERR = 67 THEN PRINT "BAD FILE NAME ERROR" : GOTO 1640
1210 IF ERL = 140 THEN OPEN L$+".LOG" FOR OUTPUT AS #3:CLOSE#3:OPEN L$+".LOG" FOR INPUT AS #1:RESUME 150
1220 IF ERR = 61 THEN PRINT "Yuk Yuk Yuk DISK FULL ERROR - You may have blown it" :GOTO 1640
1230 IF ERL=160 THEN PRINT "CHECK LIST ERROR, RECOVERING....":RESUME 180
1240 IF ERL = 580 THEN PRINT "PRINTER ERROR, ABORTING PRINTING":RESUME 590
1250 IF ERL = 720 THEN RESUME 730
1260 IF ERL = 840 THEN RESUME 850
1270 IF ERL = 1610 AND ERR = 70 THEN PRINT "DISK WRITE PROTECT ERROR":GOTO 1640
1280 IF ERR = 62 AND ERL = 200 THEN GOSUB 730:INPUT"Waiting,touch ENTER to continue ";A$:RESUME 1000
1290 IF ERR = 53 THEN PRINT "LOG FILE DOES NOT EXIST ON DEFAULT DISK DRIVE":GOTO 1640
1300 IF ERR = 72 THEN PRINT "DISK MEDIA ERROR":GOTO 1640
1310 IF ERR = 71 THEN PRINT "DISK NOT READY ERROR":GOTO 1640
1320 IF ERR = 58 THEN 1640
1330 PRINT "ERROR ";ERR;" AT LINE ";ERL:CLOSE:GOTO 1640
1340 REM RESUME 750:REM CLOSE DOWN IN AN ORDERLY MANNER
1350 INPUT "Is the contest over (Y/N) ";A$: IF LEN(A$)<1 THEN 1350
1360 IF LEFT$(A$,1)="Y" THEN 1390 ELSE 1640
1370 INPUT "What is the number of lost calls (to be added to the memory) ";O:IF O <0 THEN 1370
1380 N4 = N4+O : RETURN
1390 PRINT "CONVERTING (CLEANING UP) CONTEST LOG TO STANDARD LOG"
1400 OPEN L$+".LOG" FOR INPUT AS #1
1410 OPEN L$+".$$$" FOR OUTPUT AS #2
1420 INPUT "Do you want the contacts numbered (Y/N) ";AA$: IF LEN(AA$)=0 THEN 1420
1430 IF LEFT$(AA$,1)="Y" THEN N0=1 ELSE N0=0:IF LEFT$(AA$,1)<>"N" THEN 1420
1440 INPUT "What is the name of the contest ";X$
1450 N=1:T$=" ":B$=" ":C$="-CONTEST":R$=" ":S$=" ":P$=" ":QS$=" ":QR$=" ":GOSUB 1610
1460 IF EOF(1) THEN 1520 ' GET FIRST ENTRY
1470 GOSUB 1570:GOSUB 1580:IF C1$="/*" THEN GOSUB 1630: GOTO 1460 'REM DONT SAVE FUDGE FLAG OR PREVIOUS LINE
1480 IF EOF(1) THEN 1510 ' GET SUBSEQUENT ENTRIES
1490 GOSUB 1570:IF C1$="/*" THEN GOSUB 1630:GOTO 1460 'REM DONT SAVE FUDGE FLAG OR PREVIOUS LINE
1500 GOSUB 1590:GOSUB 1580:GOTO 1480
1510 GOSUB 1590:PRINT :PRINT N9;"Entries were corrected/deleted"
1520 CLOSE#1 : CLOSE#2
1530 NAME L$+".LOG" AS L$+".RUN"
1540 NAME L$+".$$$" AS L$+".LOG"
1550 GOTO 1640
1560 REM SUBROUTINES FOLLOW
1570 INPUT#1,D1$,T1$,B1$,C1$,R1$,S1$,M1$,P1$,QS1$,QR1$,X1$:RETURN
1580 D$=D1$:T$=T1$:B$=B1$:C$=C1$:R$=R1$:S$=S1$:M$=M1$:P$=P1$:QS$=QS1$:QR$=QR1$:X$=X1$:RETURN
1590 R$=MID$(X$,1,3):X$=MID$(X$,4,LEN(X$))
1600 IF N0=1 THEN N$=STR$(N):N$=MID$(N$,2):X$=N$+"-"+X$:N=N+1
1610 WRITE#2, D$;T$;B$;C$;R$;S$;M$;P$;QS$;QR$;X$
1620 PRINT D$;TAB(10);T$;TAB(15);C$;TAB(26);B$;TAB(30);R$;TAB(34);S$;TAB(38);M$;TAB(43);P$;TAB(48);QS$;TAB(50);QR$;TAB(52);X$:RETURN
1630 N9=N9+1:PRINT "ENTRY with ";C$;" DELETED":RETURN
1640 END
```

## CQSS.BAS

```bas
20 K = 1000 : REM number of stations in contest
30 PRINT "ARRL SWEEPSTAKES CONTEST SIMULATION VERSION 2.13"
40 Q4 = 18 : K7 = 74 : K6 = 66 : P1 = .1 : P5 = .5 : Z0 = 0 : Z1 = 1 : Z2 = 2 : Z3 = 3 : Q = (RND(Z1) * Z2)
50 N1 = Z1 : Z2 = 2 : Z3 = 3 : Z4 = 4 : Z5 = 5 : Z6 = 6 : Z7 = 7 : Z8 = 8 : Z9 = 9 : B1 = 100 : B2 = 200 : B3 = 300 : T3 = 12
60 DIM L(Z7),L$(Z5),C$(K),C(K),S(K7),S$(K7),P(85),P$(Z4),Z$(Q4),B$(Z4)
70 Z$(13) = "DUPLICATE" : B$(Z0) = "10" : B$(Z1) = "15" : B$(Z2) = "20" : B$(Z3) = "40"
80 P$(Z0) = "W" : P$(Z1) = "K" : P$(Z2) = "WA" : Q$ = "QWERTYUIOPASDFGHJKLZXCVBNM"
90 B$(Z4) = "80" : Z$(Z0) = "CALL" : Z$(Z1) = "LOG" : Z$(Z2) = "BAND " : Z$(Z3) = "CHECK" : X$ = ""
100 P$(Z3) = "WB" : Z$(Z4) = "STATUS" : Z$(Z5) = "BREAK" : Z$(Z6) = "QSL" : E$ = " DE "
110 B4 = 400 : B5 = 500 : T4 = 16 : T5 = 20 : T6 = 24 : T7 = 60 : Z$(Z8) = "REPEAT" : Z$(Z9) = "QRT"
120 DIM M$(Z5) : M$(Z0) = "NUMBER" : M$(Z1) = "POWER" : M$(Z4) = Z$(Z3) : M$(Z5) = "SECTION"
130 M$(Z2) = Z$(Z0) : A$ = "G3ZCZ/W3" : H = 21 : D = 19 : R$ = " !# * " : D$ = "NOVEMBER" : N = 1968
140 K$ = "CK" : Z$(7) = "SEND" : Z$(10) = "TUNE" : Z$(11) = "DATA " : Z$(T3) = "TIME" : M$(3) = Z$(3)
150 P$(Z4) = "N" : PRINT "COPYRIGHT (C) JOE KASSER [G3ZCZ] 1979 "
160 FOR I = Z0 TO 85 : READ P(I) : NEXT 
170 DATA .3,.58,.66,.76,.84,.88,1,.17,.34,.53,.78,1,.04,.41,.85,1,.04
180 DATA .14,.22,.32,.41,.44,.51,.59,.97,1,.11,.16,.25,.31,.62,.7,.99,1
190 DATA .11,.26,.33,.38,.69,.76,.83,.93,.97,1,.17,.23,.28,.34,.48,.52
200 DATA .95,.98,1,.61,.95,1,.48,.79,1,.16,.29,.4,.63,.82,.89,.92,1
210 DATA .11,.25,.52,.67,.72,.84,.98,1,.08,.21,.31,.42,.48,.58,.65,.79
220 DATA .89,.97,1
230 Z$(14) = M$(Z5) : Z$(15) = "HELP" : Z$(T4) = "CQ" : Z$(17) = "WORKED" : Z$(18) = "DEBUG"
240 S$(Z0) = "CT" : S$(Z1) = "EMASS" : S$(Z2) = "ME" : S$(Z3) = "NH" : S$(Z4) = "RI"
250 S$(Z5) = "VT" : S$(Z6) = "WMASS" : S$(Z7) = "ENY" : S$(Z8) = "NY - LI" : S$(Z9) = "NNJ"
260 S$(10) = "SNJ" : S$(11) = "WNY" : S$(12) = "DE" : S$(13) = "E.PA" : S$(14) = "M.DC"
270 S$(15) = "W.PA" : S$(16) = "ALA" : S$(17) = "GA" : S$(18) = "KY" : S$(19) = "NC"
280 S$(20) = "NFLA" : S$(21) = "SC" : S$(22) = "SFLA" : S$(23) = "TN" : S$(24) = "VA"
290 S$(25) = "WI" : S$(26) = "AK" : S$(27) = "LA" : S$(28) = "MISS" : S$(29) = "NM"
300 S$(30) = "N.TEX" : S$(31) = "OK" : S$(32) = "S.TEX" : S$(33) = "CZ" : S$(34) = "E BAY"
310 S$(35) = "LA" : S$(36) = "ORG" : S$(37) = "SB" : S$(38) = "SCV" : S$(39) = "SD" : S$(40) = "SF"
320 S$(41) = "SJV" : S$(42) = "SV" : S$(43) = "PCF" : S$(44) = "AZ" : S$(45) = "ID" : S$(46) = "MT"
330 S$(47) = "NV" : S$(48) = "OR" : S$(49) = "UT" : S$(50) = "WA" : S$(51) = "AL" : S$(52) = "WY"
340 S$(53) = "MI" : S$(54) = "OH" : S$(55) = "W VA" : S$(56) = "IL" : S$(57) = "IN" : S$(58) = "WSC"
350 S$(59) = "CO" : S$(60) = "IA" : S$(61) = "KS" : S$(62) = "MN" : S$(63) = "MO" : S$(64) = "NB"
360 S$(65) = "ND" : S$(66) = "SD" : S$(67) = "MAR" : S$(68) = "QB" : S$(69) = "ONT" : S$(70) = "MAN"
370 S$(71) = "SK" : S$(72) = "AB" : S$(73) = "BC" : S$(74) = "NWT"
380 INPUT "ARE YOU A HAM RADIO OPERATOR" ; I$ : IF LEFT$(I$,Z1) <> "Y" THEN 420
390 INPUT "YOUR CALL" ; A$ : IF RIGHT$(A$,Z2) <> "/3" THEN A$ = A$ + "/3"
400 INPUT "YEAR FIRST LICENSED " ; N
410 IF N < 1900 THEN PRINT "YOU MUST HAVE BEEN LICENSED BEFORE 1900" : GOTO 400
420 F$ = STR$(N - 1900) : PRINT : PRINT "OK","THE STATION CALL IS",A$
430 PRINT ,"THE QTH IS SILVER SPRING, MARYLAND " : PRINT 
440 INPUT "DO YOU NEED INSTRUCTIONS " ; I$ : IF LEFT$(I$,Z1) = "Y" THEN GOSUB 3300
450 PRINT : PRINT "SETTING UP CALLS OF CONTESTANTS"
460 PRINT "THIS IS GOING  TO TAKE ABOUT" ; INT(K / (T7 * Z2)) + Z1 ; " MINUTES"
470 PRINT "WHY DON'T YOU GET A CUP OF COFFEE OR SOMETHING"
480 FOR I = Z0 TO K - Z1 : X = RND(Z1) : FOR J = 75 TO 86 : IF X > P(J) THEN NEXT J
490 X = RND(Z1) : N = J - K7 : ON J - 75 GOTO 510,520,530,540,550,560,570,580,590,600
500 Y = Z0 : Z = Z6 : GOTO 610
510 Y = Z7 : Z = 11 : GOTO 610
520 Y = T3 : Z = 15 : GOTO 610
530 Y = 16 : Z = 25 : GOTO 610
540 Y = 26 : Z = 33 : GOTO 610
550 Y = 34 : Z = 43 : GOTO 610
560 Y = 44 : Z = 52 : GOTO 610
570 Y = 53 : Z = 55 : GOTO 610
580 Y = 56 : Z = 58 : GOTO 610
590 Y = 59 : Z = K6 : GOTO 610
600 Y = 67 : Z = K7
610 FOR T = Y TO Z : IF X > P(T) THEN NEXT T
620 Y = INT(RND(Z1) * Z5) : J$ = P$(Y) : C(I) = T : IF T > K6 THEN J$ = "VE" : N = T - K6
630 IF T = 51 THEN J$ = "KL" : GOTO 670
640 IF T = 33 THEN J$ = "KZ" : GOTO 670
650 IF T = 43 THEN J$ = "KH" : IF RND(Z1) < P1 THEN J$ = "KG" : IF RND(Z1) > P5 THEN J$ = "KM"
660 IF T = 25 THEN J$ = "KP" : IF RND(Z1) > P5 THEN J$ = "KV" : IF RND(Z1) > P5 THEN J$ = "KC"
670 C$(I) = J$ + RIGHT$(STR$(N),Z1) : GOSUB 2920 : GOSUB 2920 : GOSUB 2920
680 PRINT K - I : NEXT I : N = Z1 : PRINT "IT IS 2100 HRS ON" ; D ; " " + D$
690 PRINT "THE CONTEST HAS STARTED" + X$ : GOTO 1050
700 PRINT : IF N >= K THEN 2010
710 IF Q1 = Z0 AND RND(Z1) < P1 THEN 2930
720 IF H1 > T6 OR D >= 21 AND H > T5 THEN PRINT "CONTEST IS OVER" : GOTO 1480
730 INPUT "QRU" ; I$ : IF LEN(I$) < Z2 THEN PRINT "WHAT" + X$ : GOTO 730
740 FOR I = Z0 TO Q4 : IF I$ = LEFT$(Z$(I),LEN(I$)) THEN 760
750 NEXT : FOR I = Z0 TO Q4 - Z1 : PRINT Z$(I), : NEXT : PRINT : GOTO 730
760 IF I > Z8 THEN ON I - Z8 GOTO 1480,1490,1420,1170,1980,3270,1340,930,1750,1730
770 ON I GOTO 840,1040,1090,1140,1240,1300,1350,1450
780 Q6 = Z0 : ON Q1 GOTO 800,1670,1700,1620
790 PRINT "CALL WHOM ?" + X$ : PRINT "TRY TUNING OR PUT OUT A CQ" : GOTO 700
800 IF L(Z7) = Z0 AND RND(Z1) > P5 THEN 1030
810 PRINT "TRY SEND OR REPEAT, DON'T WASTE TIME" ; X$ : GOTO 700
820 IF L(Z6) = Z0 AND RND(Z1) < P1 THEN PRINT "REPEAT PLEASE" : GOTO 700
830 Q1 = Z0 : GOTO 700
840 IF C(P) > K7 THEN PRINT "IN THE LOG ALREADY" : GOTO 700
850 IF Q5 = Z0 THEN PRINT X$ + "WHY DON'T YOU SEND HIM HIS REPORT FIRST" : GOTO 730
860 FOR I = Z0 TO Z7 : IF L(I) = Z0 THEN I = Z1 : GOSUB 1580 : GOTO 700
870 Q7 = Z0 : NEXT : Y = C(P) : N = N + Z1
880 IF Y > K7 THEN Y = Y - B : GOTO 880
890 S(Y) = Z1 : C(P) = C(P) + B : GOSUB 920 : Q1 = Z0 : PRINT "NEXT QSO = " ; N
900 IF Q6 = Z1 AND RND(Z1) > P5 OR RND(Z1) < P1 THEN 930
910 GOTO 700
920 Q5 = Z0 : FOR I = Z0 TO Z7 : L(I) = Z0 : NEXT : RETURN
930 Q6 = Z1 : Y = T6 : GOSUB 1770 : IF Y = Z0 THEN 700
940 IF RND(Z1) < P1 THEN PRINT "FREQUENCY IS IN USE OM" : GOTO 700
950 IF RND(Z1) < P1 OR B = B4 AND H >= T5 AND RND(Z1) > P5 THEN PRINT "QRM" : GOTO 700
960 IF Q7 = Z0 THEN GOSUB 1970
970 IF Q7 = Z1 AND Q1 = Z1 THEN N1 = N1 + Z1
980 GOSUB 1900 : Q1 = Z1 : PRINT A$ ; E$ ; C$(P) : L(Z2) = Z1
990 IF C(P) > K7 AND Q6 = Z1 AND RND(Z1) < Z2 * P1 THEN 700
1000 IF C(P) > K7 AND Q6 = Z1 THEN 1020
1010 IF C(P) > K7 AND RND(Z1) > P5 THEN PRINT "WE'VE WORKED OM !" : GOTO 700
1020 IF RND(Z1) > P5 THEN PRINT : GOTO 700
1030 J$ = A$ : GOSUB 1860 : PRINT : GOTO 700
1040 PRINT Z$(Z2) ; " = " ; B$(INT(B - Z1) / B1)
1050 INPUT "WHICH BAND " ; I$ : FOR I = Z0 TO Z4 : IF I$ = B$(I) THEN 1070
1060 NEXT : FOR I = Z0 TO Z4 : PRINT B$(I), : NEXT : PRINT : GOTO 1050
1070 IF B = (Z1 + I) * B1 THEN 700
1080 B = (Z1 + I) * B1 : GOSUB 920 : GOTO 700
1090 IF L(Z2) = Z0 AND L(Z5) = Z0 THEN PRINT "CHECK WHAT" : GOTO 700
1100 IF C(P) >= B1 THEN PRINT C$(P) ; " WORKED ON " ; B$(INT(C(P) / B1) - Z1) : GOTO 700
1110 Y = C(P) : IF Y >= B1 THEN Y = Y - B1 : GOTO 1110
1120 IF L(Z5) = Z1 THEN PRINT "SECTION " ; : IF S(Y) = Z1 THEN PRINT "WORKED" : GOTO 700
1130 PRINT "NOT WORKED YET" : GOTO 700
1140 GOSUB 1150 : GOTO 700
1150 PRINT Z$(Z4) ; : PRINT TAB(T6) ; D ; D$ : PRINT Z$(Z2) ; : PRINT TAB(T6) ; 
1160 PRINT B$(INT(B/B1-Z1))
1170 PRINT "GMT. TIME" ; : PRINT TAB(T6) ; : PRINT H ; " HRS",M ; " MIN"
1180 PRINT "ELAPSED TIME" ; : PRINT TAB(T6) ; H1 ; " HRS",M1 ; " MIN" : IF I = T3 THEN 700
1190 PRINT "QSO'S" ; : PRINT TAB(T6) ; N - Z1 : PRINT M$(Z5) + "S WORKED" ; 
1200 PRINT TAB(T6) ; : Y = Z0 : FOR J = Z0 TO K7 : IF S(J) = Z1 THEN Y = Y + Z1
1210 NEXT : PRINT Y : PRINT "SCORE" ; : PRINT TAB(T6) ; Z2 * (N - Z1) * Y : PRINT "RATE" ; 
1220 IF H1 = Z0 AND M1 = Z0 THEN PRINT TAB(T6) ; " - - - " : RETURN
1230 PRINT TAB(T6) ; (N - Z1) * T7 / (H1 * T7 + M1) ; "QSO'S PER HOUR" : RETURN
1240 INPUT "HRS" ; X : INPUT "MIN" ; Y : IF X = Z0 AND Y < 15 THEN GOSUB 1580 : GOTO 1240
1250 IF X >= T6 OR Y >= T7 THEN GOSUB 1290 : IF LEFT$(I$,Z1) <> "Y" THEN 1240
1260 M = M + Y : IF M >= T7 THEN M = M - T7 : H = H + Z1
1270 H = H + X : IF H >= T6 THEN H = H - T6 : D = D + 1
1280 GOSUB 1150 : GOSUB 920 : Q1 = Z0 : GOTO 700
1290 INPUT "ARE YOU SURE YOU MEANT THAT" ; I$ : RETURN
1300 IF L(Z7) = Z1 THEN PRINT "73, SEE YOU LATER" : GOTO 700
1310 IF RND(Z1) > P1 THEN L(Z7) = Z1 : PRINT "ROGER" : GOTO 700
1320 IF RND(Z1) >= P1 THEN PRINT "DO YOU QSL ?"
1330 GOTO 700
1340 GOSUB 3300 : GOTO 700
1350 IF L(Z2) = Z0 OR Q1 = Z0 THEN PRINT X$ + "TO WHOM" : GOTO 700
1360 IF Q1 <> Z1 THEN PRINT "TUT TUT - CALL HIM FIRST" ; X$ : GOTO 700
1370 PRINT L$(Z2) ; " UR" ; N ; : PRINT "A" ; E$ ; A$ ; : PRINT " " ; K$ ; " " ; F$ ; " MD K"
1380 IF RND(Z1) < P1 THEN PRINT A$ ; " PLEASE REPEAT" ; E$ ; C$(P) : GOTO 700
1390 Q5 = Z1 : L(Z6) = Z1 : IF L(Z7) = Z1 THEN PRINT "QSL" : GOTO 700
1400 IF RND(Z1) > P5 THEN J$ = A$ : GOSUB 1860 : PRINT "DO YOU QSL ?"
1410 GOTO 700
1420 FOR I = Z0 TO Z5 : IF L(I) = Z1 THEN NEXT : PRINT " GOT IT ALL" : GOTO 700
1430 PRINT "STILL NEED", : FOR J = I TO Z5 : IF L(J) = Z0 THEN PRINT " " ; M$(J) ; 
1440 NEXT : PRINT : GOTO 700
1450 IF Q1 <> Z1 THEN PRINT "CALL HIM FIRST" + X$ : GOTO 700
1460 IF RND(Z1) > P1 THEN J$ = A$ : GOSUB 1860 : GOTO 700
1470 PRINT "SORRY OM, QRM ... TRY AGAIN" : GOTO 700
1480 PRINT "FINAL " ; : GOSUB 1150 : GOTO 3500
1490 IF B < 400 AND H > T4 AND RND(Z1) < P1 THEN GOSUB 3110 : GOTO 700
1500 IF RND(Z1) >= P5 + P1 AND B = B4 AND (H >= T5 OR H < Z6) THEN GOSUB 3170 : GOTO 700
1510 GOSUB 920 : Q1 = Z0 : Y = T7 : GOSUB 1770 : IF Y = Z0 THEN 700
1520 L(Z2) = Z1 : Q1 = INT(RND(Z1) * Z3) + Z2 : ON Q1 GOTO 1530,1540,1550
1530 PRINT "QRZ" ; E$ ; C$(P) : GOTO 700
1540 FOR T = 0 TO INT(RND(1) * 3) : PRINT "CQ CQ CQ" ; E$ ; C$(P) ; " " ; : NEXT : PRINT "K" : GOTO 700
1550 GOSUB 1810 : PRINT C$(K) ; E$ ; C$(P) : IF RND(Z1) > P5 THEN 700
1560 Q7 = Z1 : GOSUB 1970
1570 GOSUB 1900 : J$ = C$(K) : GOSUB 1860 : GOTO 700
1580 PRINT "THAT'S AGAINST THE RULES" : PRINT X$
1590 IF I = Z1 THEN PRINT "QSO NOT COMPLETED"
1600 IF I = Z5 THEN PRINT "15 MINUTE MINIMUM"
1610 RETURN
1620 IF RND(Z1) > P5 THEN 1850
1630 IF Q7 = Z1 AND RND(Z1) > P5 THEN 970
1640 IF RND(Z1) > P5 THEN Q1 = Z2 : GOTO 1530
1650 IF RND(Z1) < P5 THEN PRINT "PLEASE, I'M IN QSO" : GOTO 700
1660 PRINT C$(K) ; E$ ; C$(P) ; " SORRY QRM, REPEAT" : GOTO 700
1670 IF RND(Z1) > P5 THEN 1530
1680 IF RND(Z1) < P1 THEN Q1 = Z3 : GOTO 1540
1690 Q1 = Z1 : GOTO 960
1700 IF RND(Z1) > P5 THEN 1540
1710 IF RND(Z1) > P5 THEN Q1 = Z2 : GOTO 1530
1720 Q1 = Z1 : GOTO 960
1730 FOR J = Z0 TO K - Z1 : PRINT C$(J) ; : IF C(J) > K7 THEN PRINT " * " ; 
1740 PRINT , : NEXT : GOTO 700
1750 FOR J = Z0 TO K - Z1 : IF C(J) > K7 THEN PRINT C$(J),
1760 NEXT : GOTO 700
1770 Y = RND(Z1) * Y + Z4 : FOR T = Z0 TO Z2 : GOSUB 2860 : GOSUB 2820 : GOSUB 2030
1780 IF Y = Z0 OR RND(Z1) < P1 THEN NEXT : Y = Z0
1790 IF Y = Z1 THEN GOSUB 1900
1800 RETURN
1810 I = K : T = INT(RND(Z1) * Z3 + Z2) : J$ = P$(INT(RND(Z1) * Z3))
1820 C$(I) = J$ + RIGHT$(STR$(T),Z1) : GOSUB 2920 : GOSUB 2920 : GOSUB 2920
1830 IF C$(I) = A$ THEN 1810
1840 RETURN
1850 GOSUB 1810 : J$ = C$(K) : GOSUB 1860 : N1 = N1 + Z1 : Q7 = Z1 : GOTO 700
1860 L$(Z0) = STR$(N1) : L(Z7) = Z1 : PRINT J$ ; " UR" ; : 
1870 FOR J = Z0 TO Z5 : IF L(J) = Z1 OR RND(Z1) >= P1 THEN L(J) = Z1 : PRINT " " ; L$(J) ; : GOTO 1890
1880 PRINT R$ ; 
1890 NEXT : PRINT : RETURN
1900 FOR J = Z1 TO 26 : IF RIGHT$(C$(P),Z1) <> MID$(Q$,J,Z1) THEN NEXT 
1910 L$(Z1) = "A" : IF J >= T3 THEN L$(Z1) = "B"
1920 Y = Z0 : FOR J = Z1 TO Z3 : Y = Y + ASC(MID$(RIGHT$(C$(P),Z3),J,Z1)) : NEXT 
1930 L$(Z4) = STR$(INT(Y / Z2 - 57)) : L$(Z0) = STR$(N1)
1940 L$(Z2) = C$(P) : L$(Z3) = K$ : Y = C(P)
1950 IF Y > K7 THEN Y = Y - B1 : GOTO 1950
1960 L$(Z5) = S$(Y) : RETURN
1970 N1 = Z1 + INT(N * T3 * P1 * RND(Z1)) : RETURN
1980 IF RND(Z1) < P1 THEN 700
1990 IF RND(Z1) > P5 THEN PRINT "SORRY"
2000 Q1 = Z0 : GOTO 900
2010 PRINT "ANTENNA BLEW DOWN IN WIND"
2020 PRINT " NO WAY  TO FIX IT - QRT" : GOTO 1480
2030 IF C(P) < B1 THEN 2070
2040 X = C(P)
2050 C(P) = C(P) - B1 : IF C(P) > B1 THEN 2050
2060 GOSUB 2070 : C(P) = X : RETURN
2070 Y = 0 : IF C(P) = 14 OR C(P) = T6 THEN 2810
2080 IF C(P) < Z8 THEN 2230
2090 IF C(P) < T3 THEN 2280
2100 IF C(P) < T4 THEN 2310
2110 IF C(P) = 25 THEN 2710
2120 IF C(P) < 25 THEN 2340
2130 IF C(P) = 33 THEN 2750
2140 IF C(P) < 33 THEN 2380
2150 IF C(P) = 43 THEN 2640
2160 IF C(P) < 43 THEN 2420
2170 IF C(P) = 51 THEN 2640
2180 IF C(P) < 53 THEN 2480
2190 IF C(P) < 56 THEN 2530
2200 IF C(P) < 59 THEN 2550
2210 IF C(P) < 67 THEN 2580
2220 ON C(P) - 67 GOTO 2280,2530,2550,2580,2480,2480,2480
2230 IF B = B4 OR B = B5 AND H < T3 OR B = B3 AND H >= T5 AND RND(Z1) > P5 THEN 2810
2240 IF B = B3 AND (H < T5 AND H >= T3 OR RND(Z1) > P5 AND H >= Z8) THEN 2810
2250 IF B = B2 AND (H >= T5 OR H >= Z8 AND H < T3) AND RND(Z1) < P1 THEN 2810
2260 IF B = B1 AND (H >= T3 AND Q = Z2 AND H < T5 OR H >= T5 AND RND(Z1) > P5) THEN 2810
2270 RETURN
2280 IF B = B3 AND (H >= Z8 AND H < T3 OR H >= T5) AND RND(Z1) > P5 THEN 2810
2290 IF B >= B4 OR B = B1 AND Q = Z1 AND RND(Z1) < P5 AND H < T4 AND H >= T3 THEN 2810
2300 RETURN
2310 IF B = B5 OR B = B4 AND H < Z8 AND RND(Z1) > P5 THEN 2810
2320 IF B = B4 AND (H >= T5 OR H >= Z8 AND H < T3) THEN 2810
2330 RETURN
2340 IF B = B4 OR B = B3 AND H >= Z8 OR B = B1 AND Q = Z3 AND H >= T3 AND RND(Z1) > P5 THEN 2810
2350 IF B = B2 AND H < T5 AND H >= T3 AND RND(Z1) > P5 THEN 2810
2360 IF B = B5 AND H < T4 OR (H >= T5 OR H < T3) AND RND(Z1) > P5 THEN 2810
2370 RETURN
2380 IF B < B4 AND H >= T3 AND H < T5 OR B = B4 AND H < Z8 THEN 2810
2390 IF B = B5 AND (H < Z8 AND H >= Z4 OR H < Z4 AND RND(Z1) > P5) THEN 2810
2400 IF RND(Z1) > P5 AND (H >= T5 OR H >= Z8 AND H < T3) THEN 2810
2410 RETURN
2420 IF H < Z8 AND H >= T3 AND RND(Z1) < P1 THEN 2810
2430 IF B < B4 AND H >= T3 AND H < T5 OR B >= B3 AND H < Z8 AND H >= 4 THEN 2810
2440 IF H >= T5 AND B < B3 AND RND(Z1) > P5 THEN 2810
2450 IF H >= T5 AND (B = B3 OR B = B4 AND RND(Z1) < P1) THEN 2810
2460 IF H < Z4 AND (B = B4 OR B = B5 AND RND(Z1) > P5) THEN 2810
2470 RETURN
2480 IF B < B4 AND H < T5 AND H >= T3 THEN 2810
2490 IF H >= T5 AND B > B1 AND B < B5 AND RND(Z1) > P5 THEN 2810
2500 IF B = B4 AND (H < Z8 OR H >= T5 AND RND(Z1) > P5) THEN 2810
2510 IF B = B5 AND (H < Z8 AND H >= Z4 OR H < Z4 AND RND(Z1) > P5) THEN 2810
2520 RETURN
2530 IF B = B5 OR B = B4 AND (H >= Z8 OR RND(Z1) > P5) THEN 2810
2540 RETURN
2550 IF B = B4 OR B = B5 AND (H < Z8 OR RND(Z1) > P5 AND H < T3 OR H >= T5) THEN 2810
2560 IF B = B3 AND H >= T3 AND H < T5 THEN 2810
2570 RETURN
2580 IF B > B3 AND H < Z8 OR H < T3 AND RND(Z1) > P5 THEN 2810
2590 IF B = B1 AND Q = Z5 AND H >= T3 AND RND(Z1) > P5 THEN 2810
2600 IF B = B2 AND H < T5 AND H >= T3 AND RND(Z1) > P5 THEN 2810
2610 IF B = B3 AND (H < T5 AND H >= T3 OR H >= T5 AND RND(Z1) > P5) THEN 2810
2620 IF H >= T5 AND (B = B4 AND RND(Z1) > P5 OR B = B5 AND RND(Z1) < P1) THEN 2810
2630 RETURN
2640 IF B < B4 AND H < T5 AND H >= T4 THEN 2810
2650 IF B = B3 AND (H >= T3 AND RND(Z1) > P5 OR H >= Z8 AND H < T3 AND RND(Z1) < P1) THEN 2810
2660 IF B = B1 AND H >= T3 AND (H < T5 OR RND(Z1) > P5) THEN 2810
2670 IF B = B2 AND (H < T4 AND H >= T3 AND RND(Z1) > P5 OR H >= T5 AND RND(Z1) < P1) THEN 2810
2680 IF B = B4 AND H < Z8 AND (H >= Z4 OR RND(Z1) > P5) THEN 2810
2690 IF B = B5 AND H >= Z4 AND H < Z8 AND RND(Z1) > P5 THEN 2810
2700 RETURN
2710 IF B = B5 AND H < Z8 OR B < B4 AND H >= Z8 AND H < T5 THEN 2810
2720 IF H >= T5 AND (B = B1 AND RND(Z1) > P5 OR B = B2 OR B = B3) THEN 2810
2730 IF B = B4 AND (H >= Z4 AND H < Z8 OR H < Z4 AND RND(Z1) < P1) THEN 2810
2740 RETURN
2750 IF B < B3 AND H < T5 AND (H >= T3 AND H < T4 OR H >= Z8 AND RND(Z1) > P5) THEN 2810
2760 IF H >= T5 AND (B = B2 OR B = B4) AND RND(Z1) > P5 THEN 2810
2770 IF H >= T4 AND B = B3 OR B = B4 AND H < Z8 THEN 2810
2780 IF B = B4 AND RND(Z1) > P5 AND H < T3 THEN 2810
2790 IF B = B5 AND H < Z8 AND (H >= Z4 OR RND(Z1) > P5) THEN 2810
2800 RETURN
2810 Y = 1 : RETURN
2820 IF I = Z0 THEN P = INT(RND(Z1) * K)
2830 P = P + INT(RND(Z1) * Z9) : IF P >= K THEN P = P - K
2840 IF B = B1 AND RND(Z1) > P1 * P1 THEN Q = Q + INT(RND(Z1) + Z4) : IF Q > Z5 THEN Q = Q - Z5
2850 RETURN
2860 S1 = S1 + Y : IF S1 < T7 THEN RETURN
2870 S1 = S1 - T7 : M1 = M1 + Z1 : M = M + Z1 : IF S1 >= T7 THEN 2870
2880 IF M >= T7 THEN M = M - T7 : H = H + Z1 : GOTO 2880
2890 IF M1 >= T7 THEN M1 = M1 - T7 : H1 = H1 + Z1 : GOTO 2890
2900 IF H < T6 THEN RETURN
2910 H = H - T6 : D = D + Z1 : RETURN
2920 I$ = MID$(Q$,INT(RND(Z1) * 26) + Z1,Z1) : C$(I) = C$(I) + I$ : RETURN
2930 IF RND(Z1) > P1 * Z2 THEN 700
2940 X = INT(RND(Z1) * 10) + Z1
2950 ON X GOTO 2960,2970,2980,2960,2960,2960,2960,2960,3070,3020,2960
2960 PRINT "TIMEOUT IN THE BATHROOM" : Y = T5 : GOTO 2990
2970 PRINT "TIMEOUT  TO CLEAR UP SPILT COFFEE" : Y = Z5 : GOTO 2990
2980 PRINT "KIDS ARE FIGHTING..TIMEOUT" : Y = T4
2990 Y = INT(RND(Z1) * Y) + Z3 : PRINT "OFF THE AIR FOR " ; Y ; " MIN"
3000 IF Y > 14 THEN X = Z0 : GOTO 1260
3010 Y = Y * T7 : GOSUB 2860 : GOTO 700
3020 IF RND(Z1) * 10 < P5 THEN 2010
3030 PRINT "POWER LINE BLACKOUT" : X = INT(RND(Z1) * Z5) : Y = INT(RND(Z1) * T6) + Z8
3040 PRINT "POWER CAME BACK ON AFTER " ; X ; " HRS",Y," MIN"
3050 IF X = Z0 AND Y < 15 THEN Y =  Y * T7 : GOSUB 2860 : GOTO 700
3060 GOTO 1260
3070 PRINT "FINAL TUBES BLEW OUT"
3080 IF H < T3 THEN PRINT "WHERE ARE YOU GOING  TO GET SPARES AT THIS TIME"
3090 PRINT "LUCKY YOU, SPARES ARE  TO HAND "
3100 PRINT "REPLACING THEM NOW" : Y = T7 : GOTO 2990
3110 I$ = "YV4XYZ" : IF RND(Z1) > P5 THEN I$ = "HK3ZZZ"
3120 J$ = "MIAMI" : IF RND(Z1) > P5 THEN J$ = "LOS ANGELES"
3130 PRINT "CQ CQ CQ DE " ; I$
3140 PRINT "THIS IS " ; I$ ; " LOOKING FOR A PHONE PATCH IN TO " ; J$
3150 IF RND(Z1) < P5 - P1 THEN 3140
3160 RETURN
3170 X = INT(RND(Z1) * Z4) : IF RND(Z1) > P5 THEN 3190
3180 PRINT "YOU ARE LISTENING  TO " ; : ON X + Z1 GOTO 3200,3210,3220,3230
3190 PRINT "THIS IS " ; : ON X GOTO 3210,3220,3230
3200 PRINT "RADIO MOSCOW" : GOTO 3240
3210 PRINT "RADIO PEKING" : GOTO 3240
3220 PRINT "RADIO ALBANIA" : GOTO 3240
3230 PRINT "THE BBC" : IF RND(Z1) < P1 * Z2 THEN PRINT "THIS IS RADIO NEWSREEL" : RETURN
3240 IF RND(Z1) > P5 THEN PRINT "A NEWS BROADCAST FOLLOWS" : RETURN
3250 IF RND(Z1) > P5 THEN PRINT "YOU HAVE BEEN LISTENING  TO A COMMENTARY ON THE NEWS"
3260 RETURN
3270 PRINT Z$(14) + "S NEEDED" : J = K7 : FOR I = Z0 TO K7
3280 IF S(I) = Z0 THEN PRINT S$(I), : J = J - Z1
3290 NEXT : PRINT : PRINT J + Z1,Z$(14) + "S WORKED" : PRINT : GOTO 700
3300 PRINT Z$(Z0),"CALL OTHER STATION"
3310 PRINT Z$(Z1),"ENTER CONTACT IN LOG"
3320 PRINT Z$(Z2),"CHANGE BANDS"
3330 PRINT Z$(Z3),"CHECK  TO SEE IF WORKED"
3340 PRINT Z$(Z4),"SEE HOW WELL YOU ARE DOING"
3350 PRINT Z$(Z5),"TAKE A BREAK (TIMEOUT)"
3360 PRINT Z$(Z6),"CONFIRM RECEPTION OF CONTEST DATA "
3370 PRINT Z$(Z7),"SEND YOUR DATA  TO OTHER STATION"
3380 PRINT Z$(Z8),"REQUEST REPEAT OF OTHER STATION'S DATA "
3390 PRINT Z$(Z9),"GIVE UP AND SWITCH OFF EQUIPMENT"
3400 PRINT Z$(10),"TUNE ACROSS BAND "
3410 PRINT Z$(11),"DETERMINE WHICH DATA ARE MISSING FROM REPORT"
3420 PRINT Z$(T3),"LOOK AT TIME OF DAY"
3430 PRINT Z$(13),"TELL OTHER STATION THAT YOU HAVE WORKED BEFORE"
3440 PRINT Z$(14),"LIST SECTIONS STILL NEEDED"
3450 PRINT Z$(15),"RECEIVE SUMMARY OF INSTRUCTIONS"
3460 PRINT Z$(T4),"CALL " ; Z$(T4)
3470 PRINT Z$(17),"LIST OF STATIONS WORKED (UNSORTED)"
3480 PRINT "YOU MAY TYPE FIRST TWO LETTERS FOR ALL ENTRIES, EXCEPT",
3490 PRINT Z$(14) ; " (TYPE 3)" : RETURN
3500 PRINT "HOPE YOU ENJOYED THE CONTEST"
```

## FREQPLOT.BAS

```bas
10 REM FREQUENCY PLOTTING ROUTINE
20 DATA 1.8,2, 3.5,4, 7,7.5
30 DATA 10,10.5,14,14.5,18,18.5
40 DATA 21,21.5,24,24.5
50 DATA 28,28.5,28.5,29,29,29.5,29.5,30
60 DATA 50,50.5
70 I = 9 : REM 9 BANDS
80 LPRINT "BAND","      LOW OSC",,"     HIGH OSC"
90 FOR X = 1 TO 13
100 READ F1,F2
110 LPRINT F1, F1-I,F2-I,F1+I,F2+I
120 NEXT
```

## LOGCONV.BAS

```bas
10 WIDTH 80:PRINT "LOG CONVERSION PROGRAM 2.0 (C) JOE KASSER G3ZCZ 1985"
20 REM CONVERTS CONTEST LOG TO STANDARD LOG
30 INPUT "WHAT IS THE LOG NAME ";L$
40 CLS:N=1
50 OPEN L$+".$$$" FOR OUTPUT AS #2
60 OPEN L$+".LOG" FOR INPUT AS #1
70 IF EOF(1) THEN 160
80 INPUT#1,D$,T$,B$,C$,R$,S$,M$,P$,QS$,QR$,X$
110 R$=LEFT$(X$,2):N$=STR$(N):N$=MID$(N$,2)
120 X$=MID$(X$,3,LEN(X$)):X$=N$+"-"+X$:N=N+1
130 PRINT#2, D$;",";T$;",";B$;",";C$;",";R$;",";S$;",";M$;",";P$;",";QS$;",";QR$;",";X$
140 PRINT D$;",";T$;",";B$;",";C$;",";R$;",";S$;",";M$;",";P$;",";QS$;",";QR$;",";X$
150 GOTO 70
160 CLOSE#1 : CLOSE#2
170 NAME L$+".LOG" AS L$+".RUN"
180 NAME L$+".$$$" AS L$+".LOG"
190 END
```

## OSCFREQ.BAS

```bas
10 REM COMPUTE FREQUENCY OF AN OSCILLATOR
20 X = 50
30 Y = 500
40 Z = 25
50 INPUT "L = (uH) " ; L
60 L = L * .000001
70  LPRINT "L (uH) =" ; L * 1E+06
80 LPRINT : LPRINT
90 LPRINT "F (Mhz)","C (pF)"
100 FOR C1 = X TO Y STEP Z
110  C = C1 * 1E-12
120 F = 1 / (2*3.1419*(SQR(L*C)))
130 F1 = F * .000001
140 C2 = C * 1E+12
150 LPRINT F1,C2
160 NEXT
170 LPRINT CHR$(12)
180 GOTO 50
```

## OSCPLOT.BAS

```bas
10 REM COMPUTE OSCILLATOR FREQUENCY
20 X = 50  : REM MINIMUM VALUE OF CAPACITANCE
30 Y = 500 : REM MAXIMUM VALUE OF CAPACITANCE
40 Z = 25  : REM STEP
50 INPUT "L (uH) ";L
60 C3 = 0 : F3 = C3 : F4 = F3
70 LPRINT "L (uH) = ";L
80 L = L * .000001 : REM CONVERT uH TO H
90 LPRINT
100 LPRINT "F (Mhz)", "C (pF)", "SLOPE","DELTA F"
110 FOR C1 = X TO Y STEP Z
120 C = C1 * 1E-12 : REM CONVERT pF TO F
130 REM
140 F = 1 / ( 2 * 3.1419 * (SQR( L * C )))
150 IF C1 = X THEN F5 = F
160 F1 = F * .000001
170 F4 = F3 - F1
180 C2 = C * 1E+12
190 M = (F3 - F2) / (C3 - C2)
200 IF M = 0 THEN F4 = 0 : REM FUDGE FIRST VALUE
210 LPRINT F1,C2,M,F4
220 F3 = F1
230 C3 = C2
240 NEXT
250 F5 = F5 / F
260 LPRINT
270 LPRINT "TUNING RATIO IS";F5
```

## OSCUPLK.BAS

```bas
10 REM OSCAR UPLINK FREQUENCY PLOTTER
20 I = .02
30 REM GAP BETWEEN SAMPLES
40 LPRINT " UPLINK",,"XTALS"
50 LPRINT "--------------------------------------------------"
60 FOR F = 145.85 TO 146 STEP I
70 F1 = F/8 : F2 = F/12 : F3 = F/18
80 LPRINT F,F1,F2,F3
90 NEXT
```

## RESISTOR.BAS

```bas
20 REM charging resistor calculator
30 V2 = 3.75
40  LPRINT "INPUT VOLTAGE",6,9,12,15
50 LPRINT
60 LPRINT "RESISTANCE",,,"CURRENT (mA)"
70 FOR R = 10 TO 100 STEP 10
80 LPRINT R,
90 FOR J = 1 TO 4
100 READ V1
110 V = V1 - V2
120 I = V / R
130 I1 = I * 1000
140 LPRINT I1,
150 NEXT
160 RESTORE
170 LPRINT
180 NEXT
190 DATA 6,9,12,15
```

## UPLINK.BAS

```bas
10 REM OSCAR MODE B UPLINK FREQUENCY PLOTTER
30 I = .02
40 REM GAP BETWEEN SAMPLES
50 LPRINT "UPLINK",,"XTALS"
60 FOR F = 435 TO 436 STEP I
70 F1 = F/24 : F2 = F/36 : F3 = F/(18*3)
80 LPRINT F,F1,F2,F3
90 NEXT
```

## WHATSON.BAS

```bas
10 KEY OFF:WIDTH "LPT1:",132:W$ = "WHATSON- HF propagation Prediction Program Version 1.91"
20 ON ERROR GOTO 600
30 DIM W$(500),A3(500),A4(500),L(1),T1(1),T3(1),T4(1),C0(1),T9(1),M(1),G6(1),G7(1),G8(1)
40 WIDTH 80:CLS:LOCATE 2,9:PRINT W$
50 BLANK$ = " ":FOR I = 1 TO 80:BLANK$ = BLANK$+" ":NEXT:LA$ = "LAT.":NA$ = "  LON."
60 C1$ = CHR$(10)+CHR$(10)+CHR$(10):C2$ = CHR$(12)
70 D$ = "YyNnQq":X$ = "SsLl":Z$ = "PpSs":LA$ = "LAT.":NA$ = "LON.":P$ = "SHORT"
80 P1 = 3.14159266#
90 LOCATE 5,17:PRINT "BASED ON MINIMUF-3 PROGRAM DEVISED BY:"
100 LOCATE 7,18:PRINT "P.H LEVINE, R.B. ROSE J.N. MARTIN"
110 LOCATE 9,15:PRINT"MODIFIED BY G3CCZ,G4LH,G4GKO,4X4AS and G3ZCZ"
120 OPEN "G3ZCZ.DAT" FOR INPUT AS 1 ' GET STATION DATA
130 INPUT#1,G3ZCZ$,L5,W5:CLOSE#1
140 I% = 1:OPEN "WHATSON.DAT" FOR INPUT AS 1
150 GOSUB 700:GOSUB 2710:LOCATE 21,10:PRINT "PLEASE STANDBY WHILE PREFIX DATA ARE LOADED     "
160 IF EOF(1) THEN 180
170 INPUT#1, W$(I%),A3(I%),A4(I%):I% = I%+1:GOTO 160
180 CLOSE#1:PX = I%-1
190 CR=0:GOSUB 410:LL$=Y$
200 IF M1 = 1 THEN 230 ELSE CLS
210 ' MAIN LOOP STARTS HERE
220 J9=P1/180: LN8=99:FOR CH9 = 1 TO PX: J9=P1/180: PX$ = W$(CH9):L6 = A3(CH9):W6 = A4(CH9):GOSUB 960 :NEXT CH9:GOTO 320
230 J9=P1/180:I = 1:CLS:LOCATE 1,32:PRINT "PREFIX MENU":PRINT:FOR C = 1 TO 4:PRINT " NBR   PX";TAB(1+C*20);:NEXT: GOTO 250
240 FOR R = 4 TO 20:LOCATE R,1:PRINT BLANK$:NEXT
250 FOR C = 1 TO 4:FOR R = 4 TO 20:LOCATE R,(C-1)*20+1:PRINT I;:LOCATE R,(C-1)*20+6:PRINT "- ";W$(I):I = I+1:IF I>PX THEN I = 1            
260 NEXT:NEXT
270 LOCATE 22,1:PRINT BLANK$:LOCATE 22,1:INPUT "Pick a number corresponding to a prefix (0 will give you further choices )";CH9:IF CH9 = 0 THEN 240
280 IF CH9 = 999 THEN GOSUB 490: GOTO 230
290 IF CH9<0 OR CH9>PX THEN 270
300 PX$ = W$(CH9):L6 = A3(CH9):W6 = A4(CH9) 'SET UP DESTINATION CO-ORDINATES
310 GOSUB 960
320 IF C2$= C1$ THEN 340
330 PRINT CHR$(12):IF P%=2 THEN LPRINT CHR$(12);CHR$(18)
340 PRINT:PRINT TAB(20)"DO YOU WANT ANOTHER QTH Y/N ?"
350 K$ = INKEY$:IF K$ = "" THEN 350
360 K = INSTR(D$,K$):IF K>0 THEN 380
370 BEEP:GOTO 350
380 IF K = 1 OR K = 2 THEN GOSUB 700: GOSUB 2710:GOTO 200
390 IF K = 3 OR K = 4 THEN 690
400 BEEP:GOTO 210
410 ' CALCULATE QRA SQUARE
420 IF CR = 0 THEN P = W5:R = L5 ELSE P = W6:R = L6
430 IF P>180 THEN P = P-360
440 P = (P+180)/20:R = (R+90)/10
450 Y = INT(P):X = INT(R):P = (P-Y)*10:R = (R-X)*10:C = INT(P):D = INT(R)
460 Y$ = CHR$(Y+65)+CHR$(X+65)+CHR$(C+48)+CHR$(D+48)
470 Y$ = Y$+CHR$(INT((P-C)*24)+65)+CHR$(INT((R-D)*24)+65)
480 Y$ = Y$:FOR K = 0 TO 5: Y(K) = ASC(MID$(Y$,K+1,1)):NEXT K:RETURN
490 ' SET UP G3ZCZ FOR YOUR STATION
500 CLS: LOCATE 2,9:PRINT W$:LOCATE 5,1: INPUT "What is your Call ";AA$
510 IF LEN(AA$) = 0 THEN 520 ELSE G3ZCZ$ = AA$
520 INPUT "What is your lattitude in degrees North ";J9: IF J9 = 0 THEN 530 ELSE L5 = J9
530 INPUT "What is your Longitude in degrees East  ";J9: IF J9 = 0 THEN 540 ELSE W5 = J9
540 CLS:LOCATE 2,9: PRINT W$:LOCATE 8,1: PRINT "CALL SIGN ";TAB(30);G3ZCZ$:
550 LOCATE 10,1: PRINT "LATITUDE (Degrees North)";TAB(30);L5
560 LOCATE 12,1: PRINT "LONGITUDE (Degrees East)";TAB(30);W5
570 LOCATE 18,1: INPUT "Are the data correct (Y/N) ";AA$
580 IF LEFT$(AA$,1) = "Y" THEN 590 ELSE 500
590 OPEN "G3ZCZ.DAT" FOR OUTPUT AS 1: PRINT #1,G3ZCZ$;",";L5;",";W5: CLOSE #1: RETURN
600 ' ERROR TRAPPING ROUTINES
605 IF ERR = 62 AND ERL = 170 THEN RESUME 180
610 IF ERR <> 53 THEN 670
620 IF ERL =140 THEN BEEP:PRINT:PRINT "ERROR- WHATSON.DAT IS NOT ON LOGGED IN DRIVE":GOTO 690
630 BEEP:PRINT "ERROR- ":PRINT "G3ZCZ.DAT FILE Which holds your Geographical data IS NOT ON LOGGED IN DRIVE":PRINT:PRINT
640 INPUT " Do you want to create the file now ";AA$: IF LEN(AA$)=0 THEN 640
650 IF LEFT$(AA$,1) = "N" THEN 690 ELSE IF LEFT$(AA$,1) = "Y" THEN 660 ELSE 640
660 GOSUB 490:RESUME 140
670 IF ERR =  27 THEN PRINT "PRINTER OUT OF PAPER"
680 PRINT "ERROR ";ERR;" ON LINE ";ERL
690 END
700 CLS:LOCATE 2,10:PRINT "Are predictions for today (Y/N) "
710 K$ = INKEY$:IF K$ = "" THEN 710
720 K = INSTR(D$,K$): IF K = 1 OR K = 2 THEN 750
730 IF K = 3 OR K = 4 THEN 760
740 BEEP:GOTO 710
750 M0 = VAL(MID$(DATE$,1,2)):D0 = VAL(MID$(DATE$,4,2)): GOTO 800
760 LOCATE 4,10:INPUT "DAY";D0
770 IF D0<1 OR D0>31 THEN BEEP:GOTO 760
780 LOCATE 6,10:INPUT "MONTH(1...12) ";M0
790 IF M0<1 OR M0>12 THEN BEEP:GOTO 780
800 M0$=MID$("JanFebMarAprMayJunJulAugSepOctNovDec",(M0-1)*3+1,3)
810 LOCATE 8,10:INPUT "SUNSPOT NUMBER ";S0
820 IF ABS(W5-W6)<80 THEN K = 1: GOTO 870
830 LOCATE 10,20:PRINT"LONG OR SHORT PATH (L/S) ?"
840 K$ = INKEY$:IF K$ = "" THEN 840
850 K = INSTR(X$,K$):IF K>0 THEN 870
860 BEEP:GOTO 840
870 IF K = 1 OR K = 2 THEN P$ = "SHORT"
880 IF K = 3 OR K = 4 THEN P$ = "LONG"
890 LOCATE 12,10:PRINT"Output to printer (P) or Screen (S) ?"
900 K$ = INKEY$:IF K$ = "" THEN 900 
910 K = INSTR(Z$,K$):IF K>0 THEN 930
920 BEEP:GOTO 900 
930 IF K = 1 OR K = 2 THEN P% = 2:PL=55:C2$ = CHR$(12):GOTO 950
940 C2$ = C1$:PL=20
950 RETURN
960 IF P% = 2 THEN LPRINT CHR$(15);
970 W1 = -(W5-180*(1+SGN(W5-.001)))*J9
980 W2 = -(W6-180*(1+SGN(W6-.001)))*J9
990 L1 = L5*J9:L2 = L6*J9
1000 IF M1 = 1 THEN CLS:PRINT "G3ZCZ  "+W$:PRINT "   DAY:";D0;"  MONTH:";M0;"  SUN SPOT NUMBER:";S0
1010 IF M1 = 1 THEN IF P%=2 THEN LPRINT "G3ZCZ  "+W$:LPRINT "   DAY:";D0;"  MONTH:";M0;"  SUN SPOT NUMBER:";S0
1020 ' ROTATE LONGITUDE
1030 W3 = W2-W1+.001
1040 W3 = P1*(1-SGN(W3))+W3
1050 ' PATH LENGTH
1060 H1 = SIN(L1)*SIN(L2)+COS(L1)*COS(L2)*COS(W3)
1070 G1 = ATN (SQR(1 - H1*H1)/H1)+P1/2*(1-SGN(H1))
1080 IF P$ = "LONG" THEN G1 = P1+P1-G1
1090 IF M1= 2 THEN 1140
1100 N$ = "N":IF L5<0 THEN N$ = "S" ELSE IF L5 = 0 THEN N$ = " "
1110 IF W5 = 360 OR W5 = 0 THEN E$ = " " ELSE E$="E"
1120 PRINT:PRINT G3ZCZ$;"  LOCAL QTH IS:";LA$;L5;N$;"/";NA$;W5;ES$;" = ";LL$:PRINT
1130 IF P%=2 THEN LPRINT:LPRINT G3ZCZ$;"  LOCAL QTH IS:";LA$;L5;N$;"/";NA$;W5;ES$;" = ";LL$:LPRINT
1140 ' PATH LENGTH IN 4000 KM UNITS.
1150 H0 = INT(1.59*G1)+1
1160 ' BEARINGS.
1170 H9 = (SIN(L2)-H1*SIN(L1))/SIN(G1)/COS(L1)
1180 IF H9 < -.99999 THEN H9 = ATN(P1/2*(1-SGN(H9))):GOTO 1200
1190 H9 = ATN(SQR(1-H9*H9)/H9)+P1/2*(1-SGN(H9))
1200 H9 = H9*SGN(W3-P1)*SGN(P1-G1)
1210 H9 = H9+P1*(1-SGN(H9))
1220 BEARING=INT(H9/J9+.5):DISTANCE=INT(6356.775#*G1+.5)
1230 IF M1 = 1 THEN 1270
1240 LN8=LN8+1:IF LN8>PL THEN GOSUB 2630:LN8=1
1250 PRINT PX$;TAB(8);:IF P%=2 THEN LPRINT PX$;TAB(9);:LPRINT USING "       ###";BEARING;:LPRINT USING "  ##,###  ";DISTANCE;
1260 GOTO 1330
1270 PRINT P$;" PATH TO ";PX$;", BEARING:";INT(H9/J9+.5);"deg. ";" DISTANCE:";INT(6356.775#*G1+.5);" KM   ":PRINT
1280 IF P%=2 THEN LPRINT P$;" PATH TO ";PX$;", BEARING:";BEARING;"deg. ";" DISTANCE:";DISTANCE;" KM   ":LPRINT
1290 PRINT "DATE & TIME OF PRINTOUT:";DATE$;"/";TIME$:PRINT
1300 IF P%=2 THEN LPRINT "DATE & TIME OF PRINTOUT:";DATE$;"/";TIME$:LPRINT
1310 PRINT TAB(3)"GMT" TAB(9)"HPF" TAB(16)"FOT" TAB(23)"LUF" TAB(41)"GMT" TAB(47)"HPF" TAB(54)"FOT" TAB(61)"LUF"
1320 IF P%=2 THEN LPRINT TAB(3)"GMT" TAB(9)"HPF" TAB(16)"FOT" TAB(23)"LUF" TAB(41)"GMT" TAB(47)"HPF" TAB(54)"FOT" TAB(61)"LUF"
1330 Y6 = ATN(1/TAN(G1/(H0+1))-.952/SIN(G1/(H0+1)))
1340 IF Y6<.314 THEN Y6 = .314
1350 Y6 = 1/SQR(1-.9650001*COS(Y6)^2)
1360 Y1 = .0172*(10+(M0-1)*30.4+D0)
1370 Y2 = .409*COS(Y1)
1380 Y1 = .13*SIN(Y1)+.156*SIN(Y1+Y1)
1390 ' DIRECTION COSINE.
1400 H9 = (SIN(L1)-COS(G1)*SIN(L2))/SIN(G1)/COS(L2)
1410 ' "M" FACTOR.
1420 M9 = SIN(2.5*G1/H0)
1430 M9 = 1+2.5*M9*SQR(M9)
1440 K1 = 1-.5/H0
1450 FOR N = 0 TO 1
1460 ' CONTROL POINT
1470 L9 = COS(G1*K1)*SIN(L2)+SIN(G1*K1)*COS(L2)*H9
1480 L0 = P1/2-(ATN(SQR(1-L9*L9)/L9)+P1/2*(1-SGN(L9)))
1490 W0 = (COS(G1*K1)-SIN(L2)*L9)/COS(L2)/COS(L0)
1500 IF W0 = 0 THEN W0 = .0000001
1510 W0 = ATN(SQR(1-W0*W0)/W0)+P1/2*(1-SGN(W0))
1520 W0 = P1-SGN(P1-G1*K1)*(P1-W0)
1530 W0 = W3+W0*SGN (W3-P1)*SGN(P1-G1)+W1-.001
1540 W0 = W0-P1*(1-SGN(P1+P1-W0))
1550 ' LOCAL NOON.
1560 T0 = 3.82*W0+12+Y1
1570 T0 = T0-12*(1+SGN(T0-24))*SGN(ABS(T0-24))
1580 IF COS(L0+Y2)>-.26 THEN 1620
1590 T1(N) = 0
1600 GOTO 1740
1610 ' DURATION OF SUNLIGHT.
1620 T1(N) = (-.26+SIN(Y2)*L9)/(COS(Y2)*COS(L0)+.001)
1630 T1(N) = 12-ATN(T1(N)/SQR(ABS(1-T1(N)*T1(N))))*24/P1
1640 ' T(dawn)
1650 T7 = T0-T1(N)/2
1660 T3(N) = T7+12*(1-SGN(T7))*SGN(ABS(T7))
1670 ' T(sunset)
1680 T7 = T0+T1(N)/2
1690 T4(N) = T7-12*(1+ SGN(T7-24))*SGN(ABS(T7-24))
1700 C0(N) = ABS(COS(L0+Y2))
1710 ' RELAXATION TIME.
1720 T9(N) = 9.7*(C0(N)^8)
1730 IF T9(N)<.1 THEN T9(N) = .1
1740 K1 = 1-K1
1750 ' F0F 2 MULTIPLIER.
1760 M(N) = M9*.75*((12/T1(N)-1)*SGN(INT(12/T1(N)))+1)
1770 M(N) = M(N)*(1+S0/100*(1-(T1(N)/12-1)*SGN(INT(T1(N)/12))))
1780 L9 = ABS(L0+.21*SIN(W0+.35))
1790 G2 = .5
1800 IF L9<P1/4 THEN 1830
1810 M(N) = M(N)*(1-.1*(1+COS(L9*4)))
1820 G2 = .2
1830 L(N) = SIN(L9*4)*G2
1840 ' EFECTIVE COS(X) PRESETS.
1850 G8(N) = P1*T9(N)/T1(N)
1860 T7 = T1(N)/T9(N)
1870 IF T7>85 THEN T7 = 85
1880 G7(N) = C0(N)*G8(N)*(EXP(-T7)+1)
1890 G6(N) = G7(N)*EXP ((T1(N)-24)/2)
1900 NEXT N
1910 FOR T5 = 0 TO 23
1920 J9 = 100
1930 K9 = 0
1940 FOR N = 0 TO 1
1950 G0 = 0
1960 G3 = P1/2
1970 IF T1(N) = 0 THEN 2160
1980 IF T4(N)<T3(N) THEN 2030
1990 ' DAY TIME?
2000 IF (T5-T3(N))*(T4(N)-T5)>0 THEN 2050
2010 GOTO 2210
2020 ' NIGHT TIME?
2030 IF (T5-T4(N))*(T3(N)-T5)>0 THEN 2210
2040 ' EFECTIVE COS(X)(day)
2050 T6 = T5+12*(1+SGN(T3(N)-T5))*SGN(ABS(T3(N)-T5))
2060 G4 = P1*(T6-T3(N))/T1(N)
2070 T8 = (T3(N)-T6)/T9(N)
2080 IF ABS(T8)>85 THEN T8 = 85*SGN(T8)
2090 G0 = C0(N)*(SIN(G4)+G8(N)*(EXP(T8)-COS(G4)))
2100 G3 = P1/2
2110 IF T6-T3(N)>T1(N)/2+3 THEN 2130
2120 G3 = (T6-T3(N))/(T1(N)/2+3)*G3
2130 G3 = G3*(1+SGN(L(N)))
2140 IF G0<G6(N) THEN G0 = G6(N)
2150 ' F0F 2
2160 G2 = SQR(7+45*SQR(G0/(1+G8(N)*G8(N))))
2170 ' HPF
2180 G2 = G2*M(N)*1.27*(1+SIN(G3)*L(N))
2190 GOTO 2270
2200 ' EFECTIVE COS(X)(night)
2210 T6 = T5+12*(1+SGN(T4(N)-T5))*SGN(ABS(T4(N)-T5))
2220 G4 = P1*(T6-T4(N))/(24-T1(N))
2230 G0 = G7(N)*EXP((T4(N)-T6)/2)
2240 G3 = G4+(P1-G4)/4*(1+SGN(L(N)))
2250 G4 = 0
2260 GOTO 2160
2270 IF G2<J9 THEN J9 = G2
2280 ' E LAYER.
2290 Y8 = .2: IF T1(N) = 0 THEN 2360
2300 IF T1(N)*G4 = 0 THEN 2360
2310 Y9 = C0(N)*SIN(P1*(T6-T3(N))/T1(N))
2320 IF Y9 > .174 THEN 2350
2330 Y8 = (ATN(SQR(1-Y9*Y9)/Y9)*180/P1-76)^(-.4)
2340 GOTO 2360
2350 Y8 = Y9 ^ (.3)
2360 Y9 = (3.4+.00544*S0)*Y8*Y6
2370 IF Y9>7 THEN 2400
2380 Y9 = .9099999*Y9-.37
2390 GOTO 2410
2400 Y9 = (1.33*Y9-3.31)^(2)/7
2410 IF K9<Y9 THEN K9 = Y9
2420 NEXT N
2430 F1=(J9+.5):F2=((J9/1.27)*.8499999)+5 :F3=K9+.5
2440 IF M1 = 1 THEN GOSUB 2540 ELSE GOSUB 2480 ' PRINT LINE OF DATA
2450 NEXT T5 :IF M1=1 THEN 2470
2460 PRINT :IF P%=2 THEN LPRINT
2470 RETURN
2480 IF F1<F2 THEN PRINT "-- "; :GOTO 2500
2490 IF F3<F2 THEN PRINT USING "## ";F2; ELSE PRINT "++ ";
2500 IF P%=2 THEN 2510 ELSE 2530
2510 IF F1<F2 THEN LPRINT "-- "; :GOTO 2530
2520 IF F3<F2 THEN LPRINT USING "## ";F2; ELSE LPRINT "++ ";
2530 RETURN
2540 PRINT USING"  ##";T5;:IF F1< F2 THEN PRINT  "     ------------                 ";:GOTO 2570
2550 IF F2< F3 THEN PRINT  "     ++++++++++++                 ";:GOTO 2570
2560 PRINT USING"###### ";INT(F1);INT(F2);INT(F3);:PRINT "             ";
2570 IF P%=2 THEN 2580 ELSE 2610
2580 LPRINT USING"  ##";T5;:IF F1< F2 THEN LPRINT  "     ------------                 ";:GOTO 2610
2590 IF F2< F3 THEN LPRINT  "     ++++++++++++                 ";:GOTO 2610
2600 IF P%=2 THEN LPRINT USING"###### ";INT(F1);INT(F2);INT(F3);:LPRINT "             ";
2610 IF T5/2<>INT(T5/2) THEN PRINT :IF P%=2 THEN LPRINT
2620 RETURN
2630 PRINT P$;" PATH OPTIMAL FREQUENCY PREDICTIONS FOR ";G3ZCZ$;" ON ";D0;M0$:PRINT 
2640 IF P%=2 THEN LPRINT C2$:LPRINT:LPRINT P$;" PATH OPTIMAL FREQUENCY PREDICTIONS FOR ";G3ZCZ$;" ON ";D0;M0$
2650 PRINT "SUNSPOT NUMBER ";S0:PRINT:IF P%=2 THEN LPRINT "SUNSPOT NUMBER ";S0:LPRINT
2660 PRINT "PX      1  2  3  4  5  6  7  8  9 10 11 12 13 14 15 16 17 18 19 20 21 22 23 24"
2670 IF P%=2 THEN LPRINT "   PX    BEARING DISTANCE   1  2  3  4  5  6  7  8  9 10 11 12 13 14 15 16 17 18 19 20 21 22 23 24"
2680 PRINT "------------------------------------------------------------------------------"
2690 IF P%=2 THEN LPRINT "------------------------------------------------------------------------------------------"
2700 RETURN
2710 LOCATE 17,1:PRINT "Which Mode, Single Px or Contest Sheet (S/C) ? "
2720 K$ = INKEY$:IF K$ = "" THEN 2720
2730 M1= INSTR("SC",K$):IF M1=0 THEN BEEP: GOTO 2720
2740 RETURN
```

{% comment %}samples_end{% endcomment %}

### Directory of PC-SIG Library Disk #0562

     Volume in drive A has no label
     Directory of A:\

    ADVERT            5376   1-21-88  11:09p
    CONTEST  ARC     14637  12-28-87   3:22a
    CQSS     ARC     11462  12-28-87   3:24a
    DISK     DOC       589   1-21-88  11:33p
    FILE0562 TXT      1834   6-28-89   1:50p
    GO       BAT        38   7-08-87  12:19a
    GO       TXT       386   6-28-89   1:53p
    LOGBOOK  ARC    101357   1-21-88   9:12p
    PKX35A35 EXE     70784   5-30-87  10:47p
    READ     ME       1181   1-21-88  11:27p
    README   BAT        39   6-28-89   1:52p
    REGISTER ME       1408   1-21-88  11:11p
    SOFT4HAM ARC     57495   9-20-87  10:42p
    WHATSON  ARC     15841  12-28-87   3:25a
           14 file(s)     282427 bytes
                           30720 bytes free
