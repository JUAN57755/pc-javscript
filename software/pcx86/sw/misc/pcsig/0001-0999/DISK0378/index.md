---
layout: page
title: "PC-SIG Diskette Library (Disk #378)"
permalink: /software/pcx86/sw/misc/pcsig/0001-0999/DISK0378/
machines:
  - id: ibm5170
    type: pcx86
    config: /machines/pcx86/ibm/5170/cga/1024kb/rev3/machine.xml
    diskettes: /machines/pcx86/diskettes.json,/disks/pcsigdisks/pcx86/diskettes.json
    autoGen: true
    autoMount:
      B: "PC-SIG Library Disk #0378"
    autoType: $date\r$time\rB:\rDIR\r
---

{% include machine.html id="ibm5170" %}

{% comment %}info_begin{% endcomment %}

## Information about "DICTIONARY"

    This disk is full of tools to aid the aspiring writer.  If you wish to
    check your spelling, we have a nice BASIC-based dictionary program.
    Along with it there is a simple text editor included, as well as word-
    counters and complexity analyzers.  For anyone interested in improving
    the quality of their writing, this is a good investment.
    
    System Requirements: Some programs require BASIC
    
    How to Start: Consult the .DOC files for instructions.  To run programs
    suffixed .COM or .EXE: enter their name and <ENTER>, i.e., for
    WORDFREQ.EXE, type WORDFREQ and hit <ENTER>.  To run the PC-SPELL.BAS
    type BASICA PC-SPELL and hit <ENTER>.
    
    Suggested Registration:  FSED $35.00; FOGFIN $15.00
    
    File Descriptions:
    
    SETFSED  DOC  Documentation for SETFSED.COM
    SETFSED  COM  FSED installation program
    FSED     DOC  Manual for FSED
    FSED     COM  FSED V2.0 Full Screen Editor by D.W. Daetwyler
    CHOP     EXE  Subdivides text files - Ver 1.1 by W.J. Kennamer
    INDEX    EXE  Creates index of word & phrases marked with control codes
    INDEX    DOC  Documentation for INDEX.EXE
    FOGFIN   DOC  Documentation for FOGFIN.COM
    FOGFIN   COM  Measures writing complexity using Gunning Fog Index
    WORDFREQ BAS  Source for WORDFREQ.EXE
    WORDFREQ EXE  Graphs word length frequency for a writing sample
    DICTIONA      PC-SPELL dictionary of correctly spelled words
    SORTDIC  BAS  Adds words to PC-SPELL dictionary
    PC-SPELL UNI  Part of PC-SPELL
    PC-SPELL DOC  Documentation to PC-SPELL V1.0
    PC-SPELL SOR  Part of PC-SPELL dictionary oriented spelling checker
    TALLY    EXE  Counts characters, words, & lines (similar to UNIX WC)
    PC-SPELL BAS  Ver 1.0 of ASCII file spelling checker by Andy Wildenberg
    FSEDIT   EXE  A simple full screen editor called the Stopgap Editor
    FSEDIT   DOC  Summary of MicroComputing article about Stopgap Editor
    TALLY    DOC  Documentation for TALLY.EXE
{% comment %}info_end{% endcomment %}

{% comment %}samples_begin{% endcomment %}

## PC-SPELL.BAS

```bas
10 ON ERROR GOTO 390
20 KILL"misspell":LOCATE ,,1
30 KILL"qwertyui"
40 ON ERROR GOTO 0
50 INPUT "drive for pc-spell disk ";DDISK$
60 IF INSTR(DDISK$,":")=0 THEN PRINT "bad drive name. please use colon.":GOTO 50
70 INPUT "drive for work disk";WDISK$
80 IF INSTR(WDISK$,":")=0 THEN PRINT "bad drive name. please use colon.":GOTO 70
90 IF DDISK$=WDISK$ AND (DDISK$="a:" OR DDISK$="A:") THEN
100 REM word lister
110 A$=WDISK$+"*.*"
120 FILES A$:KEY OFF
130 INPUT FILE$
140 OPEN WDISK$+FILE$ FOR INPUT AS #1
150 DIM WORD$(2000):CLS:W=1
160 WHILE NOT EOF(1) AND FRE(0)>2500:LOCATE 1,1:PRINT TIME$;
170 GOSUB 280:LINE INPUT#1,A$:IF A$="" THEN 240 'skip blank line
180 COUNT=COUNT+1
190 X=1
200 IF LEN(A$)<X THEN 240
210 CHAR$=MID$(A$,X,1)
220 IF ASC(CHAR$)<=64 THEN GOSUB 280 ELSE GOSUB 320
230 X=X+1:GOTO 200
240 WEND
250 IF EOF(1) THEN COUNT=0:RESET:GOTO 360
260 RESET:GOTO 360
270 REM found end of word (punctuation)
280 IF CHAR$="'" THEN 320 ' leave apostraphies in words.
290 IF LEN(WORD$)>1 THEN WORD$(W)=WORD$:W=W+1:LOCATE 25,1:PRINT "word # ";W;:WORD$="" ELSE WORD$=""
300 RETURN
310 REM another letter or '
320 IF CHAR$>"@" AND CHAR$<"[" THEN CHAR$=CHR$(ASC(CHAR$)+32)
330 WORD$=WORD$+CHAR$
340 RETURN
350 IF CHAR$>="A" THEN 320 ELSE 280
360 LOCATE 5,1:COLOR 24,7:PRINT "Cleaning out memory...please be patient";:COLOR 7,0
370 LOCATE ,1:PRINT SPACE$(79);
380 COMMON WORD$(),W,WDISK$,DDISK$:CHAIN DDISK$+"pc-spell.sor"
390 RESUME NEXT
```

## SORTDIC.BAS

```bas
10 ON ERROR GOTO 260
20 KILL"qwertyui"
30 NAME "dictionary" AS "qwertyui"
40 ON ERROR GOTO 280
50 OPEN "qwertyui" FOR INPUT AS #1
60 OPEN "update" FOR INPUT AS #2
70 OPEN "dictionary" FOR OUTPUT AS #3
80 INPUT #1,DIC$:INPUT #2,M$
90 REM start main loop
100 IF M$<DIC$ THEN 190
110 REM dictionary word is smaller
120 PRINT #3,DIC$:W=W+1
130 I=1:PRINT "D";:INPUT #1,DIC$
140 IF M$>DIC$ THEN 120
150 PRINT #3,M$:W=W+1
160 I=2:PRINT "m";:INPUT #2,M$
170 GOTO 100
180 REM merge word is smaller
190 PRINT #3,M$:W=W+1
200 I=2:INPUT #2,M$:PRINT "M";
210 IF DIC$>M$ THEN 190
220 I=1:PRINT "d";:PRINT #3,DIC$:W=W+1
230 INPUT #1,DIC$
240 GOTO 100
250 END
260 PRINT ERR,ERL:RESUME NEXT
270 REM end of one of the files
280 PRINT "finishing ...";:IF I=2 THEN 370
290 REM dictionary ran out of words
300 WHILE NOT EOF(2)
310 PRINT #3,M$:W=W+1
320 INPUT #2,M$
330 WEND:RESET
340 PRINT "the dictionary now consists of";W;"words"
350 KILL"update":END
360 REM merge file ran out
370 WHILE NOT EOF(1)
380 PRINT #3,DIC$:W=W+1
390 INPUT #1,DIC$
400 WEND:RESET
410 GOTO 340
```

## WORDFREQ.BAS

```bas
80 DEFINT A-Y:DEFSNG Z:OPTION BASE 1
90 WIDTH 80: WIDTH "LPT1:",255
100 KEY OFF: CLS
110 BUFFERSIZE=500
120 DIM T$(BUFFERSIZE),FREQ(101), CHECK$(7), SCALE(7)
130 CHECK$(1)=",":CHECK$(2)=".":CHECK$(3)="?":CHECK$(4)="!":CHECK$(5)=":":CHECK$(6)=";":CHECK$(7)="-"
140 SCALE(1)=1:SCALE(2)=2:SCALE(3)=5:SCALE(4)=10:SCALE(5)=25:SCALE(6)=50:SCALE(7)=100
150  TXTBASE=8:MAX=20:GRAPHBASE=22:STATBASE=GRAPHBASE-(MAX+1)
160 COUNT = 0
180 GOSUB 2800
190 IF CGA=FALSE THEN 250
200 SCREEN 0,1,0,0
210 MENU=0:TYPE=1:GRAPH=2
220 FOR SCR=1 TO 3: SCREEN ,,SCR,MENU:CLS:NEXT:SCREEN ,,MENU,TYPE
250 ON ERROR GOTO 0:CLS
260 GOSUB 2570
270 IF COLR THEN FG=(FG+3) MOD 7:BDR= (BDR+2) MOD 15: COLOR FG,0,BDR
280 LOCATE TXTBASE,20:PRINT "1.  Type New Text"
290 PRINT TAB(20) "2.  Store Text on Disk"
300 PRINT TAB(20) "3.  Load Text from Disk"
310 PRINT TAB(20) "4.  Tally Frequencies & Bar Graph"
320 PRINT TAB(20) "5.  Display Text on Screen or Printer"
330 COLOR 7,0:LOCATE TXTBASE+8,20: PRINT "Position ";:COLOR 31,0:PRINT CHR$(175) " " CHR$(174);:COLOR 7,0: PRINT " with cursor arrow up and down keys"
340 LOCATE TXTBASE+10,20 :PRINT "then press F1 to choose, F10 to end":COLOR 7,0
360 LOCATE 25,25:COLOR 7,0:PRINT "F1 ";:COLOR 0,7:PRINT "CHOOSE";:COLOR 7,0
370 PRINT "         F10 ";:COLOR 0,7:PRINT "END";:COLOR 7,0
400 ON ERROR GOTO 0: IF CGA THEN SCREEN  ,,MENU,MENU:CURSCR=MENU
410 GOSUB 570
420 KEY(11) ON: KEY(14) ON:KEY(1) ON:KEY(10) ON
430 ON KEY(1) GOSUB 490: ON KEY(10) GOSUB 670
440 ON KEY(11) GOSUB 550: ON KEY(14) GOSUB 610
460 POKE 106,0:K$=INKEY$:GOTO 460
490 LOCATE TXTBASE+COUNT, 15:PRINT CHR$(32);:LOCATE TXTBASE+COUNT,60:PRINT CHR$(32)
500 BEEP:KEY(11) OFF: KEY(14) OFF
510 ON COUNT +1 GOSUB 710,890,1110,1390,2060
520 IF CGA THEN RETURN 400 ELSE RETURN 250
550 LOCATE TXTBASE+COUNT,15:PRINT CHR$(32);:LOCATE TXTBASE+COUNT,60:PRINT CHR$(32)
560 IF COUNT THEN COUNT=COUNT-1
570 LOCATE TXTBASE+COUNT,15:COLOR 31,0:PRINT CHR$(175);:LOCATE TXTBASE+COUNT,60:PRINT CHR$(174):COLOR 7,0
580 RETURN
610 LOCATE TXTBASE+COUNT,15:PRINT CHR$(32);:LOCATE TXTBASE+COUNT,60:PRINT CHR$(32)
620 COUNT = (COUNT+1) MOD 5
630 LOCATE TXTBASE+COUNT,15:COLOR 31,0:PRINT CHR$(175);:LOCATE TXTBASE+COUNT,60:PRINT CHR$(174):COLOR 7,0
640 RETURN
670 IF GCA THEN SCREEN 0,1,0,0
680 CLS:COLOR 7,0,0:KEY ON:PRINT "ENDING...." :END
710 IF GCA THEN SCREEN ,,TYPE,CURSCR
720 CLS: IF COLR THEN COLOR 14,0,14 ELSE COLOR 15,0
730 FLNM$=""
740 PRINT TAB(6) "Type up to " BUFFERSIZE " lines of text, not longer than 253 letters each"
750 PRINT TAB(6) "Type <ENTER> at end of line.  Type $$$$ to signal end of input.":PRINT
760 COLOR 7,0
770 IF GCA THEN SCREEN ,,TYPE,TYPE:CURSCR=TYPE
780 LNKT=0
790 FOR I = 1 TO BUFFERSIZE
800 IF I =(BUFFERSIZE\10)*9 THEN PREVLOC=CSRLIN:LOCATE 25,30:COLOR 23,0:PRINT "Buffer getting full!!";:LOCATE PREVLOK,1:COLOR 7,0:
810 PRINT USING"***  ";I;
820 LINE INPUT T$(I)
830 IF INSTR(T$(I),"$$$$") THEN LNKT=I: I=BUFFERSIZE:GOTO 850
840 IF I = BUFFERSIZE THEN ERRMSG$=STR$(BUFFERSIZE)+" line buffer is full.":T$(BUFFERSIZE)= LEFT$(T$(BUFFERSIZE),249)+"$$$$":LNKT=I:LOCATE 25,30:GOSUB 2750
850 NEXT
860 RETURN
890 IF GCA THEN SCREEN ,,TYPE,CURSCR
900 CLS:IF COLR THEN COLOR 14,0,14 ELSE COLOR 7,O
910 IF LNKT=0 THEN ERRMSG$= "No text to save": CLS:LOCATE 4,8: IF GCA THEN SCREEN ,,TYPE,TYPE:GOSUB 2750:RETURN ELSE GOSUB 2750: RETURN
920 IF GCA THEN SCREEN ,,TYPE,TYPE:CURSCR=TYPE
930 FRM$= STRING$(40,223):LOCATE 3,8:PRINT FRM$:LOCATE 5,8:PRINT FRM$
940 LOCATE 4,8
950 INPUT "What is the file name you'd like to use ";FLNM$
960 IF FLNM$=" " THEN RETURN
970 COLOR 7,0
980 IF INSTR(FLNM$,".")= 0 AND (RIGHT$(FLNM$,4)<>".TXT" OR RIGHT$(FLNM$,4)<> ".txt") THEN FLNM$=FLNM$+".TXT"
990 ON ERROR GOTO 2640
1000 OPEN FLNM$ FOR INPUT AS # 1
1010 CLOSE:BEEP:LOCATE 6,8:ERRMSG$= "File already exists!":GOSUB 2750:RETURN 
1030  ON ERROR GOTO 0 
1040 OPEN FLNM$ FOR OUTPUT AS # 1
1050 FOR I = 1 TO LNKT
1060 PRINT #1,T$(I)
1070 NEXT I
1080 CLOSE:RETURN
1110 IF CGA THEN SCREEN ,,TYPE,CURSCR
1120 CLS:COLOR 15,0:LOCATE 4,8:PRINT "<Enter> returns you to main menu.":PRINT
1130 IF COLR THEN FG = (FG+1) MOD 6:BDR= (BDR+1) MOD 15:COLOR GF,0,BDR ELSE COLOR 7,0
1140 FRM$=STRING$(7,32)+STRING$(58,220):PRINT FRM$
1150 PRINT TAB(8) "Type the file name of the text you want to load or"
1160 PRINT TAB(8) "type A, B, C, or D to see the .TXT file directory on that drive."
1170 PRINT FRM$
1180 IF CGA THEN SCREEN ,,TYPE, TYPE:CURSCR=TYPE
1190 LOCATE 17,8:PRINT SPACE$(71): IF COLR THEN FG = (FG+1) MOD 7:BDR = (BDR+1) MOD 15:COLOR FG,0,BDR
1200 LOCATE 17,8 :INPUT "",FLNM$
1210 IF FLNM$= "" THEN RETURN 
1230 ON ERROR GOTO 2680 
1240 IF LEN(FLNM$) = 1 AND INSTR("AaBbCcDd",FLNM$) THEN FLNM$= FLNM$+ ":*.TXT":LOCATE 11,1:FILES FLNM$:GOTO 1190 
1250 IF INSTR(FLNM$,".") = 0 AND ((RIGHT$(FLNM$,4)<>".TXT" OR RIGHT$(FLNM$,4)<> ".txt")) THEN FLNM$ = FLNM$+".TXT" 
1260 OPEN FLNM$ FOR INPUT AS #1 
1270 ON ERROR GOTO 0
1290 FOR I= 1 TO BUFFERSIZE
1300 LNKT= I-1
1310 IF EOF(1) THEN I= BUFFERSIZE :GOTO 1340 
1320 LINE INPUT #1, T$(I)
1330 IF INSTR(T$(I),"$$$$") THEN LNKT = I: I= BUFFERSIZE 
1340 NEXT
1350 CLOSE
1360 IF CGA THEN RETURN 400 ELSE RETURN 250
1390 IF CGA THEN SCREEN ,,GRAPH,CURSCR:CURSCR=GRAPH
1400 CLS:IF LNKT= 0 THEN LOCATE 4,8:ERRMSG$= "No text to tally":IF CGA THEN SCREEN ,,GRAPH,GRAPH:GOSUB 2750: RETURN ELSE GOSUB 2750: RETURN
1420 SCKT = 1:MAXSCALE = MAX*SCALE(SCKT): ZTOTALW = 0:LONGW = 0 
1430 ZSUM= 0: ZSUMSQR = 0 :MODE= 101: ZTEMP= 0: MEDIAN = 0 
1440 GOSUB 2320 
1450 ERASE FREQ: DIM FREQ(101)
1460 IF CGA THEN SCREEN ,,GRAPH,GRAPH
1480 FOR I= 1 TO LNKT
1490 WORK$= LEFT$(T$(I),253)+" " 
1500 IF INSTR(WORK$,"$$$$") THEN I = LNKT : WORK$= LEFT$(WORK$,INSTR(WORK$,"$$$$")-1)+" " 'Check for end of text"
1510 IF LEN(WORK$)= 1 THEN 1770 
1530 FOR CHAR = 1 TO 7 
1540 FOUND = 1 
1550 WHILE FOUND
1560 FOUND = INSTR(WORK$,CHECK$(CHAR)) 
1570 IF FOUND THEN MID$(WORK$,FOUND,1) = " " 
1580 WEND
1590 NEXT
1600 STARTW= 1 
1620 ENDW = INSTR(STARTW,WORK$," ") 
1630 IF ENDW= 0 THEN 1770 
1640 WORDLEN= ENDW-STARTW: IF WORDLEN = 0 THEN 1760 
1650 FREQ(WORDLEN) = FREQ(WORDLEN)+ 1:ZTOTALW = ZTOTALW + 1 
1660  LOCATE STATBASE+1,69:PRINT USING "##,###";ZTOTALW; 
1670 IF WORDLEN >20 THEN LONGW= LONGW + 1: LOCATE STATBASE+2,71: PRINT USING"#,###";LONGW;: GOTO 1760 
1680 IF FREQ(WORDLEN)<= MAXSCALE THEN 1750 
1700 SCKT =SCKT+1: MAXSCALE=MAX*SCALE(SCKT) 
1710 IF CGA THEN GRAPH= 5-GRAPH:SCREEN ,,GRAPH, CURSCR
1720 GOSUB 2320: GOSUB 2470 
1730 IF CGA THEN SCREEN ,,GRAPH,GRAPH: CURSCR = GRAPH
1750 YPOS= FREQ(WORDLEN)\SCALE(SCKT): IF YPOS THEN LOCATE GRAPHBASE-YPOS,16+(WORDLEN*3):PRINT CHR$(219); 
1760  STARTW = ENDW +1: GOTO 1620 
1770 NEXT  
1790 IF ZTOTALW =<2 THEN LOCATE STATBASE+5,55:PRINT "No text to tally":GOTO 2010 
1800 LOCATE STATBASE+5,55: COLOR 31,0: PRINT "Calculating statistics":COLOR 7,0 
1820 FOR WORDLEN= 1 TO 100
1830 ZSUM = ZSUM + WORDLEN*FREQ(WORDLEN) 
1840 ZSUMSQR= ZSUMSQR + WORDLEN*WORDLEN*FREQ(WORDLEN) 
1850 IF FREQ(MODE)< FREQ(WORDLEN) THEN MODE = WORDLEN 
1860 IF MEDIAN = 0 THEN ZTEMP= ZTEMP + FREQ(WORDLEN): IF ZTEMP=>ZTOTALW/2 THEN  MEDIAN = WORDLEN 
1870 NEXT
1890 ZMEAN = ZSUM/ZTOTALW  
1900 ZVARIANCE = (ZSUMSQR - (ZSUM*ZMEAN))/(ZTOTALW-1): ZSDEV = SQR(ZVARIANCE) 
1920 LOCATE STATBASE+5,55: PRINT SPACE$(24);
1930 IF COLR THEN COLOR 14,0,6
1940 LOCATE STATBASE+6,55: PRINT USING "       Mode =    ##";MODE;
1950 LOCATE STATBASE+7,55: PRINT USING "     Median =    ##";MEDIAN;
1960 LOCATE STATBASE+8,55: PRINT USING "Arith. Mean =    ##.##";ZMEAN;
1970 LOCATE STATBASE+9,55: PRINT USING "Stand. Dev. =    ##.##";ZSDEV;
1980 LOCATE STATBASE+10,55: PRINT USING "       Skew =   ###.##";3*(ZMEAN-MEDIAN)/ZSDEV; 
1990 IF FLNM$<>" " THEN LOCATE STATBASE+11,53: PRINT "File name = ";FLNM$;
2000 IF COLR THEN COLOR 3
2010 LOCATE 25,5:PRINT "Hit any key to continue";
2020 SOUND 1200,5 
2030 K$= INPUT$(1): IF CGA THEN RETURN 400 ELSE RETURN 250
2060  IF CGA THEN SCREEN ,,TYPE,CURSCR
2070 CLS:IF LNKT = 0 THEN ERRMSG$ = "No text to show":LOCATE 4,8:IF CGA THEN SCREEN ,,TYPE,TYPE:GOSUB 2750:RETURN ELSE GOSUB 2750: RETURN
2080 IF COLR THEN COLOR 9,0,3 ELSE COLOR 7,0
2090 LOCATE 1,8:PRINT "<Enter> returns you to the main menu.":PRINT
2100 PRINT TAB(8)"Show text on (S)creen or(P)rinter and screen";
2110 COLOR 7,0
2120 IF CGA THEN SCREEN ,,TYPE,TYPE:CURSCR=TYPE
2130 LOCATE 3,53:PRINT SPACE$(27):LOCATE 3,53:SHOW$=INPUT$(1)
2140 IF ASC(SHOW$)= 13 THEN RETURN
2150 IF INSTR("SsPp",SHOW$)= 0 THEN BEEP:GOTO 2130 
2160 CLS
2170 FOR I= 1 TO LNKT
2180 PRINT USING "###  ";I; 
2190 IF LEN(T$(I))<74 THEN PRINT T$(I):GOTO 2220 ELSE PRINT LEFT$(T$(I),74) 
2200 WORK$= MID$(T$(I),75)
2210 IF LEN(WORK$)<74 THEN PRINT TAB(6) WORK$ ELSE PRINT TAB(6) LEFT$(WORK$,74): WORK$= MID$(WORK$,75):GOTO 2210 
2220  IF SHOW$<>"p" AND SHOW$<>"P" THEN 2260 ELSE LPRINT USING "###  ";I; 
2230 IF LEN(T$(I))< 74 THEN LPRINT T$(I):GOTO 2260 ELSE LPRINT LEFT$(T$(I),74)
2240 WORK$= MID$(T$(I),75)
2250 IF LEN(WORK$)<74 THEN LPRINT TAB(6) WORK$ ELSE LPRINT TAB(6) LEFT$(WORK$,74): WORK$= MID$(WORK$,75):GOTO 2250
2260 NEXT
2270 IF COLR THEN COLOR 11,0,0
2280 PRINT:PRINT TAB(6) "All done.  Hit any key to continue":TA$=INPUT$(1)
2290 RETURN
2320 CLS:COLOR 7,0:FRM$ = STRING$(22,205)
2330 LOCATE STATBASE,53:PRINT CHR$(201) FRM$ CHR$(187)
2340 LOCATE STATBASE+1,55:PRINT USING   "Total words = ##,###";ZTOTALW;
2350 LOCATE STATBASE+2,55:PRINT USING " Words >20 = #,###";LONGW;
2360 LOCATE STATBASE+3,53:PRINT CHR$(200) FRM$ CHR$(188)
2370 LOCATE GRAPHBASE+1,5:PRINT "Word Size-> 1  2  3  4  5  6  7  8  9  0  1  2  3  4  5  6  7  8  9  0" 
2380 FOR KT= 1 TO 9
2390 LOCATE GRAPHBASE-14+KT,5:PRINT MID$("FREQUENCY",KT,1); 
2400 NEXT
2410 FOR KT=0 TO MAX-1
2420 LOCATE (GRAPHBASE-MAX)+KT,9:PRINT USING "####";(MAX-KT)*SCALE(SCKT);:PRINT      CHR$(186); 
2430 NEXT
2440 RETURN
2470 FOR BAR = 1 TO 20
2480 IF FREQ(BAR) THEN BARTOP = FREQ(BAR)\SCALE(SCKT) ELSE GOTO 2530
2490 IF BARTOP = 0 THEN 2530
2500 FOR M = 1 TO BARTOP
2510 LOCATE GRAPHBASE-M,16+(BAR*3):PRINT CHR$(219)
2520 NEXT
2530 NEXT
2540 RETURN
2570 FRM$=STRING$(45,196):COLOR 7,0
2580 LOCATE TXTBASE-3, 18, 0 : PRINT CHR$(214) FRM$ CHR$(183) 
2590 PRINT TAB(18) CHR$(186);:COLOR 15,0:PRINT "W O R D  F R E Q U E N C Y  A N A L Y S I S  ";:COLOR 7,0:PRINT CHR$(186) 
2600 PRINT TAB(18) CHR$(211) FRM$ CHR$(189)
2610 RETURN
2640 IF ERL = 1000 AND ERR = 53 THEN RESUME 1030
2650 IF ERR = 1000 AND (ERR = 52 OR ERR = 64) THEN ERRMSG$ = FLNM$+" is not an acceptable file name":LOCATE 6,8: GOSUB 2750: RESUME 1080
2660 IF ERR = 1000 THEN ON ERROR GOTO 0
2680 IF ERR=53 AND ERL=1240 THEN ERRMSG$="No .TXT files on drive "+ LEFT$(FLNM$,1):LOCATE 17,8:GOSUB 2750: RESUME 1190
2690 IF ERR=71 AND (ERL=1240 OR ERL=1260) THEN ERRMSG$="The disk drive door isn't closed. Close it.": LOCATE 17,8: GOSUB 2750: RESUME 1190
2700 IF ERL = 1260 AND ERR = 53 THEN ERRMSG$ =FLNM$+" does not exist":LOCATE 17,8:GOSUB 2750:RESUME 1190
2710 IF ERL = 1260 AND (ERR=64 OR ERR=52) THEN  ERRMSG$=FLNM$+" is not an acceptable file name." :LOCATE 17,8: GOSUB 2750:RESUME 1190
2730 ON ERROR GOTO 0 
2750 IF COLR THEN COLOR 20,0 ELSE COLOR 31,0
2760 PRINT ERRMSG$;:COLOR 7,0 
2770 FOR I=1 TO 3000:NEXT I:RETURN
2800 TRUE = -1:FALSE = NOT TRUE
2810 DEF SEG =&HB000 
2820 GOSUB 3250
2830 IF LOCATED THEN HASMONO = TRUE
2850 DEF SEG = &HB800 
2860 GOSUB 3250
2870 IF LOCATED THEN HASCGA = TRUE
2880 DEF SEG 
2900 IF (NOT HASCGA) AND HASMONO THEN CGA = FALSE: RETURN 
2910 IF HASCGA AND (NOT HASMONO) THEN CGA = TRUE:GOSUB 3030: RETURN 
2920 IF (HASCGA AND HASMONO) THEN GOSUB 2960: RETURN
2930 BEEP: END 
2960 CLS:GOSUB 2570: LOCATE 12,12:PRINT "Press 'C' to use color monitor, 'M' for your monochrome display."
2970 LOCATE 14,12,1:CHOICE$ = INPUT$(1)
2980 IF INSTR("CcMm",CHOICE$) = FALSE THEN BEEP:GOTO 2970 
2990 IF CHOICE$ ="C" OR CHOICE$ = "c" THEN GOSUB 3100 ELSE GOSUB 3160
3000 RETURN
3030 CLS:GOSUB 2570:LOCATE 12,14:PRINT "Press 'C' for a color display, 'B' for black and white"
3040 LOCATE 14,14,1:CHOICE$=INPUT$(1)
3050 IF INSTR("CcBb",CHOICE$) = FALSE THEN BEEP:GOTO 3040 
3060 IF CHOICE$ = "C" OR CHOICE$ = "c" THEN COLR= TRUE ELSE COLR= FALSE
3070 RETURN
3100 CLS: WIDTH 80: DEF SEG = 0: POKE &H410,(PEEK(&H410) AND &HCF) OR &H20
3110 WIDTH 40:SCREEN 1:SCREEN 0:WIDTH 80:LOCATE ,,1,6,7
3120 CGA = TRUE 
3130  RETURN
3160 CLS:WIDTH 40:DEF SEG = 0: POKE &H410,PEEK (&H410) OR &H30
3170 WIDTH 80: LOCATE ,,1,12,13
3180 CGA = FALSE 
3190  RETURN
3250 LOCATED = FALSE
3260 ORIGVAL = PEEK (0) 
3280 TESTVAL = 0: POKE 0,TESTVAL:TRIALVAL = PEEK (0)
3290 IF TRIALVAL=TESTVAL THEN FIRSTTEST = TRUE ELSE FIRSTTEST= FALSE
3310 TESTVAL = 255: POKE 0,TESTVAL: TRIALVAL=PEEK(0)
3320 IF TRIALVAL=TESTVAL THEN SECONDTEST = TRUE ELSE SECONDTEST = FALSE
3340 LOCATED=FIRSTTEST AND SECONDTEST 
3350 POKE 0,ORIGVAL 
3360 RETURN
```

{% comment %}samples_end{% endcomment %}

### Directory of PC-SIG Library Disk #0378

     Volume in drive A has no label
     Directory of A:\

    FSED     COM     10240   6-26-95
    FSED     DOC     16934   1-05-85   7:36p
    SETFSED  COM      1408  11-25-84   7:58a
    SETFSED  DOC       896   2-26-95
    TALLY    EXE     11776  12-25-84   7:53p
    TALLY    DOC      5632  10-26-94
    FSEDIT   DOC      9969   4-10-83   7:36p
    FSEDIT   EXE     24576   2-23-83  10:03p
    PC-SPELL BAS      1280   9-15-84   1:25a
    PC-SPELL SOR       640   9-15-84   1:25a
    PC-SPELL DOC      3328   9-15-84   1:25a
    PC-SPELL UNI       768   9-15-84   1:25a
    SORTDIC  BAS       896   9-15-84   1:25a
    DICTIONA         33536   9-15-84   1:26a
    WORDFREQ EXE     36480  10-06-83   8:34p
    WORDFREQ BAS      9984  10-02-83   1:15p
    FOGFIN   COM     15360   9-28-84   3:16p
    FOGFIN   DOC      7576   6-11-84   7:37p
    INDEX    DOC     14427   3-05-84   7:37p
    INDEX    EXE     19712   3-05-84   8:18a
    CHOP     EXE     14848   2-22-85   7:15p
    FILES378 TXT      1650   7-08-85   2:38p
           22 file(s)     241916 bytes
                           71680 bytes free
