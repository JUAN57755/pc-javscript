---
layout: page
title: "PC-SIG Diskette Library (Disk #313)"
permalink: /software/pcx86/sw/misc/pcsig/0001-0999/DISK0313/
machines:
  - id: ibm5170
    type: pcx86
    config: /machines/pcx86/ibm/5170/cga/1024kb/rev3/machine.xml
    diskettes: /machines/pcx86/diskettes.json,/disks/pcsigdisks/pcx86/diskettes.json
    autoGen: true
    autoMount:
      B: "PC-SIG Library Disk #0313"
    autoType: $date\r$time\rB:\rDIR\r
---

{% include machine.html id="ibm5170" %}

{% comment %}info_begin{% endcomment %}

## Information about "BUSINESS SAMPLER #3"

    Time Management is the theme of this business collection.  Highlights
    include: the PC Yearbook which is a calendar/appointment schedule
    program written in C; it also includes a notepad.  IPM.COM is a
    (Critical Path Method) project scheduler written in Turbo Pascal.
    This is a methodology for determining the critical task that, if not
    accomplished by a certain time, contributes directly to the delay of
    the whole project.  It does not have a calendar function or a project
    tracking function.
    
    UserLog is a system utility designed to maintain a disk-based file of
    computer usage, thus eliminating the need for a written log that
    monitors the business and personal use of a computer.  Program Time
    calculates the amount of computer time used.  It allows apportionment
    of the time between business and non-business use.
    
    System Requirements:  BASIC
    
    How to Start: Consult the .DOC and README files for directions and
    documentation.  To run the BASIC programs, consult the directions in
    GETTING STARTED for your configuration.  To run a program suffixed
    .COM or .EXE, just type its name, i.e., for IPM.COM, type IPM <ENTER>.
    
    Suggested Registration:   IPM $15.00; USERLOG $25.00
    
    File Descriptions:
    
    KEITH2   CPM  Sample data file for IPM.COM
    ACRS     DOC  Documentation for ACRS
    ACRS     BAS  Depreciation Calculating Program
    KEYPAD   BAS  Keypad Practice Typing Program
    IPM      DOC  Documentation for IPM
    IPM      COM  Ivy League Project Manager
    CALCULA  BAS  Large Screen Calculator
    PCYEARBK DOC  Day Month and Year Calendar Program
    PCYEARBK EXE  Documentation for PCYEARBK
    PERT3    BAS  PERT/Path Critical Scheduling Program
    PRGTIM   BAS  Utility Program for PRGTIM
    PRGTIM   EXE  Computer Utilization Program
    RPTUSE   BAS  Utility Program for PRGTIM
    PRGTIM   DOC  Documentation for PRGTIM
    TMSTRT   EXE  Utility Program for PRGTIM
    TMSTRT   BAS  Utility Program for PRGTIM
    USERLOG  DOC  Documentation for USERLOG
    USERLOG  COM  Maintains Disk-Based File of Computer Usage
    TAXDEDCT BAS  Income Tax Deduction Recording Program
    USE-TIME DOC  General Documentation for TMSTRT and PRGTIM
{% comment %}info_end{% endcomment %}

{% comment %}samples_begin{% endcomment %}

## ACRS.BAS

```bas
10 REM -- ACRS PROGRAM
20 REM -- BY L. AND S. BROUDY
25 REM -- SEPT. 1982
27 REM -- from: Creative Computing, Feb. 1983 pg 183
30 N=1
40 CLS:KEY OFF:COLOR 0,7:LOCATE 3,8:PRINT"DEPRECIATION (ACRS) PROGRAM":            COLOR 7,0:PRINT
50 INPUT "WOULD YOU LIKE TO READ DATA FROM DISK    (Y/N)";ANS$
60 IF ANS$="Y" OR ANS$="y" THEN 710
70 IF ANS$="N" OR ANS$="n" THEN S=1
80 REM -- INSERTING NEW INFORMATION
90 INPUT "PLEASE TYPE IN YOUR NAME, AND YOUR SOCIAL SECURITY NUMBER (NAME,SS#)";   NM$,SS$
100 INPUT "ITEM";IT$(N)
110 PRINT "IS THE RECOVERY PERIOD FOR ";IT$(N);" 3 YRS, 5 YRS, OR 10 YRS";:         INPUT C(N)
115 IF C(N)=3 OR C(N)=5 OR C(N)=10 THEN GOTO 120 ELSE 110
120 INPUT "UNADJUSTED BASIS";BP(N)
130 INPUT "DATE PLACED IN SERVICE";S$(N)
140 INPUT "% USED FOR BUSINESS";B(N)
150 INPUT "DEPRECIATION YEAR (1,2,ETC.)";Y
160 IF C(N)=3 THEN GOSUB 410
170 IF C(N)=5 THEN GOSUB 470
180 IF C(N)=10 THEN GOSUB 530
190 INPUT "WOULD YOU LIKE A PRINTOUT";ANS$
195 IF ANS$="Y" OR ANS$="y" THEN GOSUB 850
200 REM -- SCREEN DISPLAY
210 CLS: PRINT C(N)"- YEAR ACRS"
220 PRINT "COST RECOVERY FOR  ";IT$(N)
230 PRINT "UNADJUSTED BASIS: ";:PRINT USING "$$#####.## ";BP(N)
240 PRINT:PRINT "DATE PLACED IN SERVICE: ";S$(N):PRINT "DEPRECIATION YEAR:";Y
250 PRINT:PRINT "RATE OF ACRS DEPRECIATION: ";100*R;"%"
260 DEP=BP(N)*R
270 PRINT:PRINT "FULL ACRS DEPRECIATION AMT, =";:PRINT USING "$$#####.##";DEP
280 PRINT "PERCENT OF USE FOR BUSINESS:";B(N);"%":B=0.01*B(N)
290 TD=B*DEP
300 PRINT:PRINT "ACRS DEPRECIATION DEDUCTION = ";:PRINT USING "$$#####.##";TD
310 INPUT "DO YOU WANT ANOTHER DEPRECIATION YEAR   (Y/N)";A$
320 IF A$="Y" OR A$="y" THEN GOTO 150
330 IF A$="N" AND S=1 OR A$="n" AND S=1 THEN 370
340 INPUT "DO YOU WANT TO REVIEW ANOTHER ITEM FROM THE DISK  (Y/N)";A$
350 IF A$="Y" OR A$="y" THEN 780
360 IF A$="N" OR A$="n" THEN N=M ELSE 340
370 INPUT "DO YOU WANT TO ENTER ANOTHER ITEM (Y/N)";AN$
380 IF AN$="Y" OR AN$="y" THEN N=N+1:S=1:GOTO 100
390 INPUT "DO YOU WANT TO SAVE YOUR DATA (Y/N)";A$
400 IF A$="Y" OR A$="y" THEN 610 ELSE END
410 REM -- 3 YEAR ACRS
420 IF Y=1 THEN R=0.25
430 IF Y=2 THEN R=0.38
440 IF Y=3 THEN R=0.37
450 IF Y>3 THEN GOTO 150
460 RETURN
470 REM -- 5 YEAR ACRS
480 IF Y=1 THEN R=0.15
490 IF Y=2 THEN R=0.22
500 IF Y>2 AND Y<6 THEN R=0.21
510 IF Y>5 THEN GOTO 150
520 RETURN
530 REM -- 10 YEAR ACRS
540 IF Y=1 THEN R=0.08
550 IF Y=2 THEN R=0.14
560 IF Y=3 THEN R=0.12
570 IF Y>3 AND Y<7 THEN R=0.1
580 IF Y>6 AND Y<11 THEN R=0.08999999
590 IF Y>10 THEN GOTO 150
600 RETURN
610 REM --DISK ACCESS
620 M=N
630 INPUT "WHAT WOULD YOU LIKE YOUR NAME TO FILE";F$
640 OPEN F$+".TAX" FOR OUTPUT AS #1
650 WRITE #1,M,NM$,SS$
660 FOR I=1 TO M
670 WRITE #1,IT$(I),BP(I),S$(I),B(I),C(I)
680 NEXT I
690 CLOSE #1
700 END
710 INPUT "WHAT FILE WOULD YOU LIKE TO READ";F$
720 OPEN F$+".TAX" FOR INPUT AS #1
730 INPUT #1,M,NM$,SS$
740 FOR I=1 TO M
760 NEXT I
770 CLOSE #1
780 CLS:PRINT"ITEMS IN THE FILE ARE "
790 FOR I=1 TO M
800 PRINT I,IT$(I)
810 NEXT I
820 INPUT "WHICH ITEM WOULD YOU LIKE TO REVIEW (ENTER ITEM # OR 0 FOR NEW ITEM)";I
830 PRINT IT$(I): N=I: IF I=0 THEN N=M: GOTO 370
835 IF I<=M THEN GOTO 150
840 PRINT "THE ITEM YOU CHOSE ISN'T IN THE FILE":FOR K=1 TO 1500:NEXT: GOTO 780
850 REM -- PRINTER SUBROUTINE
860 LPRINT C(N) "-YEAR DEPRECIATION (ACRS) WORKSHEET"
870 LPRINT NM$;"..................SS# ";SS$
880 LPRINT
890 LPRINT "COST RECOVERY FOR  ";IT$(N)
900 LPRINT
910 LPRINT "UNADJUSTED BASIS: ";:LPRINT USING "$$#####.## ";BP(N)
920 LPRINT "DATE PLACED IN SERVICE: ";S$(N):LPRINT"DEPRECIATION YEAR: ";Y
930 LPRINT "RATE OF ACRS DEPRECIATION: ";100*R;"%"
940 DEP=BP(N)*R
950 LPRINT:LPRINT "FULL ACRS DEPRECIATION AMT. =";:LPRINT USING "$$#####.##";DEP
960 LPRINT "PERCENT OF USE FOR BUSINESS: ";B(N);"%": B=0.01*B(N)
970 TD=B*DEP
980 LPRINT:LPRINT "ACRS DEPRECIATION DEDUCTION = ";:LPRINT USING "$$#####.##";TD
985 LPRINT
990 GOTO 310
7507 50 INPUT #1,IT$(I),BP(I),S$(I),B(I),C(I)
```

## CALCULA.BAS

```bas
40   REM COPYRIGHT 1983-RALPH JACKSON.
50   REM PRINTING - INDICATING CALCULATION PROGRAM: CALC
51   DIM TSAV#(99), TSAVESU#(99)
52   DEFINT L
54   OPEN "RECORDS" AS #1 LEN = 50
55   FIELD #1, 2 AS IR$, 20 AS DESCR$, 9 AS AMT$, 10 AS DT$, 9 AS SPARE$
60   D$=DATE$: T$=TIME$
65   DEFDBL T,N
66   DEF SEG = &H40:POKE &H17, PEEK(&H17) OR 96 'SET CAPS LOCK & NUM LOCK
71   GOSUB 73
72   GOTO 98
73   CLS: PRINT "THE IBM PERSONAL COMPUTER PROGRAM - CALC"
74   PRINT "SIMULATING A CALCULATOR WITH LARGE SCREEN"
76   PRINT "A MEMORY OF 99 NUMBERS, DISK MEMORY"
78   PRINT "AND OPTIONAL PRINTER OUTPUT.
80   PRINT:   PRINT "PROGRAMMER: R. JACKSON " D$ " " T$
81   PRINT:PRINT "MENU - E = END COMPUTATION"
82   PRINT "       S = CLEAR SUBTOTAL TO ZERO"
83   PRINT "       T = CLEAR SUBTOTAL AND TOTAL TO ZERO"
84   PRINT "       MN= STORE  DATA IN  POSITION  N>0<100"
85   PRINT "       RN= RECALL DATA FROM POSITION N>0<100"
86   PRINT "       AN= ADD TO TOTAL & SBTOT FROM N>0<100"
87   PRINT "       FN= ENTER DATA IN DISK RECORD N>0<100"
88   PRINT "       PN= PUT   DATA IN DISK RECORD N>0<100"
89   PRINT "       GN= GET DATA FROM DISK RECORD N>0<100"
96   LOCATE 1,48,0:PRINT "OPERATION       TOTAL    SUBTOTAL"
97   RETURN
98   LOCATE 16,1,0: PRINT:INPUT "ENTER Y TO OBTAIN PRINTER OUTPUT==> ",Y$
99   IF Y$="Y" THEN GOTO 400
100  TSUB=0
101  TOTAL = 0
109  M=1
110  LOCATE 19,1,0:PRINT "                                        ":LOCATE 19,1,0: INPUT "ENTER + - * / OR MENU AND VALUE==> ",F$
120  OPER$=LEFT$(F$,1)
130  V$=MID$(F$,2)
140  NUMBER=VAL(V$): N=NUMBER: GOTO 470
141  IF OPER$="F" THEN GOTO 550
142  IF OPER$="E" THEN PRINT:PRINT "END":LPRINT "END":DEF SEG = &H40: POKE &H17, PEEK(&H17) AND 223: END 'UNLOCK NUM LOCK
143  IF OPER$="M" THEN GOTO 430
144  IF OPER$="R" THEN GOTO 440
145  IF OPER$="S" THEN TSUB=0:GOTO 309
146  IF OPER$="P" THEN GOTO 550
147  IF OPER$="G" THEN GOTO 550
148  IF OPER$="A" THEN GOTO 440
149  IF OPER$="T" THEN TOTAL=0: TSUB=0:GOTO 309
151  IF OPER$="+" THEN TOTAL=TOTAL+NUMBER:TSUB=TSUB+N: GOTO 300
152  IF OPER$="-" THEN TOTAL=TOTAL-NUMBER:TSUB=TSUB-N: GOTO 300
154  IF OPER$="*" THEN TOTAL = TOTAL*NUMBER:TSUB=TSUB*N: GOTO 300
156  IF OPER$="/" THEN TOTAL = TOTAL/NUMBER:TSUB=TSUB/N: GOTO 300
158  LOCATE 19,1,0: INPUT "ENTER CORRECT FUNCTION AND NUMBER==> ",F$:GOTO 120
300  LOCATE M+1,46,0: PRINT USING "\\######.##  #######.##  #######.##";OPER$,NUMBER,TOTAL,TSUB
301  IF Y$="Y" THEN LPRINT USING "\\########.##  #########.##  #########.##";OPER$,NUMBER,TOTAL,TSUB
303 BLANK$="                              ": LOCATE M+2,46,0: PRINT USING"\                                 \";BLANK$: GOTO 310
309  LOCATE M+1,46,0: PRINT USING "\    \                               ";F$:IF Y$="Y" THEN LPRINT USING "\    \";F$
310  M=M+1: IF M>20 THEN M=1
315  LOCATE 19,1,0:PRINT "                                            "
320  GOTO 110
400  PRINT: PRINT "READY PRINTER, LEAVE 1/2 INCH TOP MARGIN"
401  A$="    OPERATION":B$="TOTAL":C$="SUBTOTAL"
402  PRINT "PRESS RETURN":INPUT " ",KX:
403  LPRINT USING "    \              \  \        \";D$,T$:LPRINT
410  LPRINT USING "\                  \  \       \  \        \";A$,B$,C$
415  GOTO 100
430  IF N<1 THEN GOTO 158
432  IF N>99 THEN GOTO 158 ELSE GOTO 500
435  LOCATE M+1,46,0:  PRINT USING "\    \";F$: M=M+1:IF Y$="Y" THEN LPRINT USING "\    \";F$ ELSE GOTO 436
436  M=M+1: IF Y$="Y" THEN GOTO 460
440  IF N<1 THEN GOTO 158
442  IF N>99 THEN GOTO 158 ELSE GOTO 443
443  IF OPER$="A" THEN GOTO 745
445 L=N: TOTAL = TSAV#(L): TSUB = TSAVESU#(L): GOTO 300
470  IF OPER$="0" GOTO 480
471  IF OPER$="1" GOTO 480
472  IF OPER$="2" GOTO 480
473  IF OPER$="3" GOTO 480
474  IF OPER$="4" GOTO 480
475  IF OPER$="5" GOTO 480
476  IF OPER$="6" GOTO 480
477  IF OPER$="7" GOTO 480
478  IF OPER$="8" GOTO 480
479  IF OPER$="9" GOTO 480 ELSE GOTO 141
480  OPER$="+": V$=MID$(F$,1): NUMBER = VAL(V$): N=NUMBER: GOTO 151
500  LET L=N: LET TSAV#(L)=TOTAL: LET TSAVESU#(L)=TSUB: GOTO 300
550  IF N<1 THEN GOTO 110
551  IF N>99 THEN GOTO 110
552  CLS: A$ = "PRESENT CONTENTS OF RECORD "
553  PRINT USING "\                        \##"; A$, N: PRINT
555  GET #1,N
559  AMNT$=AMT$
560  IR = CVI(IR$)
561  TAMT# = CVD(AMT$)
565  COLOR 0, 7:PRINT "RECNO DESCRIPTION                AMOUNT       DATE      ":COLOR 1, O
570  PRINT USING "  ##  \                    \    #######.##  \          \";IR, DESCR$,TAMT#,DT$
575  COLOR 2, 0:PRINT
576  IF OPER$ = "P" THEN GOTO 650
577  IF OPER$ = "G" THEN GOTO 750
580  INPUT "ENTER NEW DESCRIPTION (ENTER \ IF NO CHANGE)",DESC$
582  IF DESC$ = "\" THEN GOTO 586
584  LSET DESCR$ = DESC$
586  INPUT "ENTER NEW AMOUNT (ENTER \ IF NO CHANGE)",AMNT$
587  IF AMNT$ = "\" THEN GOTO 590 ELSE GOTO 588
588  AMT#=VAL(AMNT$)
589  LSET AMT$=MKD$(AMT#)
590  INPUT "ENTER NEW DATE (ENTER \ IF NO CHANGE)",D$
592  IF D$ = "\" THEN GOTO 596
594  LSET DT$ = D$
596  LSET IR$ = MKI$(N)
600  PUT #1, N
605  GOSUB 73
606  TX#=AMT#
607  GOSUB 720
610  GOTO 100
650  B$ = " AMOUNT IN TOTAL = "
655  PRINT USING "\                  \######.##"; B$, TOTAL:PRINT
660  PRINT "ENTER R IF TOTAL IS TO REPLACE AMOUNT"
662  PRINT "ENTER A IF TOTAL IS TO ADD TO AMOUNT"
664  INPUT "ENTER C TO CANCEL OPERATION"; X$
666  IF X$="R" THEN GOTO 680
668  IF X$="A" THEN GOTO 690
670  IF X$="C" THEN GOSUB 73
672  GOTO 100
680  LSET AMT$=MKD$(TOTAL#):TX#=TOTAL#: GOTO 700
686  LSET DT$=D$
690  AMT#=CVD(AMT$)
695  TOT#=TOTAL# + AMT#:TX#=TOT#
696  PRINT USING "\                  \#######.##"; B$, TOT#: INPUT " PRESS RETURN TO CONTINUE",X$
697  LSET AMT$=MKD$(TOT#): GOTO 700
700  LSET IR$=MKI$(N)
701  LSET DT$=DATE$
702  PUT #1, N
705  GOSUB 73
707  GOSUB 720
710  GOTO 100
720  IF Y$="Y" THEN LPRINT USING "\\ ## \                    \    #######.##  \          \";OPER$, IR, DESCR$, TX#, DT$
721  RETURN
745  L=N:TOTAL#=TOTAL# + TSAV#(L)
746  TSUB = TSUB + TSAVESU#(L)
747  NUMBER = TSAV#(L): GOTO 300
750  TOTAL# = CVD(AMT$)
751  INPUT "PRESS RETURN TO CONTINUE", X$
752  GOSUB 720
753  GOSUB 73
754  TSUB = TOTAL: M=2
756  LOCATE M,46,0: PRINT USING "\\######.##  #######.##  #######.##";OPER$,NUMBER, TOTAL, TSUB
758  GOTO 110
```

## KEYPAD.BAS

```bas
11 'Written by Kurt Riegel, March 1983. Telephone 703-522-5427.
12 'Keypad practice program, to develop proficiency in using keypad.
13 'Suggested filename is KEYPAD.BAS
14 DEFINT I:A$=TIME$:RANDOMIZE VAL(MID$(A$,4,2)+MID$(A$,7,2))
15 DIM X$(20):KEY OFF:CLS:PRINT TAB(30)"KEYPAD PRACTICE":PRINT
16 PRINT"Key in the numbers just below those listed, as accurately as you can."
17 PRINT:PRINT"Remember to put the NUM LOCK key in the correct state."
18 FOR I=1 TO 15:X$(I)=MID$(" 1234567890 .+-",I,1):NEXT
19 LOCATE 10,15:Y$="":FOR I=1 TO 50:Y$=Y$+X$(1+INT(RND*15)):NEXT
20 PRINT CHR$(218);STRING$(50,196);CHR$(191)		  'top of box
21 LOCATE 13,15:PRINT CHR$(192);STRING$(50,196);CHR$(217) 'bottom of box
22 LOCATE 11,1:PRINT "Your lesson "CHR$(26)" "CHR$(179);Y$;CHR$(179):PRINT" Type here  "CHR$(26)" "CHR$(179):LOCATE 12,66:PRINT CHR$(179)
23 LOCATE 12,16:LINE INPUT Z$:FOR I=LEN(Z$)+1 TO 50:Z$=Z$+" ":NEXT
24 IN=LEN(Z$): IF IN<=50 THEN IN=50
25 FOR I=1 TO IN:IF MID$(Y$,I,1)=MID$(Z$,I,1) THEN MID$(Z$,I)=" " ELSE MID$(Z$,I)=CHR$(24)
26 NEXT:LOCATE 12,1:PRINT"   Errors   "CHR$(26)" "CHR$(179);Z$:PRINT:PRINT"Hit any key to continue."
27 IF INKEY$="" THEN 27
28 LOCATE 10,1:FOR I=1 TO 5:PRINT STRING$(79," "):NEXT:GOTO 19 'last statement
```

## PERT3.BAS

```bas
5 COLOR 7,0,0
10 '     PROGRAM: ENHANCED PERT          SOURCE: BYTE, MAY, 1982, PG. 469ff.
20 '     FUNCTION: PERT-CHARTING         DATE:   5/1/82
30 '     VERSION:  1.1                   AUTHOR: T. V. BONOMA
40 '
42 SCREEN 0,1,0,0:WIDTH 80
45 DEFDBL M,X,P
47 KEY OFF
50 CLEAR 5000: CLS
60 WIDTH 80: COLOR 14,1,1:CLS:LOCATE 1,23:PRINT "PERT/CRITICAL PATH SCHEDULING"
70 LOCATE 3,23: PRINT "ORIGINAL BY S.M. ZIMMERMAN/L.M. CONRAD"
80 LOCATE 5,23:  PRINT "MODIFIED/ADAPTED FOR IBM PC BY"
90 LOCATE 7,23:  PRINT "THOMAS V. BONOMA   MAY, 1982"
95 LOCATE 9,23:PRINT"MODIFIED BY PATRICK HERON JUNE, 1983"
100 PRINT:PRINT"Note:  Beginning Events will be sorted numerically"
105 FOR X=1 TO 2000:NEXT X
120 DIM A$(500,2),A(500,11),SV(11)
130 CLS: LOCATE 5,25:  PRINT "MENU FOR INPUT CHOICES"
135 FOR HOLDIT = 1 TO 700:NEXT HOLDIT
140  LOCATE 7,20: PRINT "K - KEYBOARD"
150 LOCATE 8,20: PRINT "D - DISK FILE"
155 LOCATE 9,20:PRINT"F - TO DISPLAY FILES"
170 LOCATE 14,40: INPUT "What is your selection  ";IO$
172 IF IO$="F" OR IO$="f" THEN GOTO 18050
175 IF IO$<>"D"  AND IO$<>"K" AND IO$<>"d"  AND IO$<>"k"        THEN 130
180  IF IO$<>"K" AND IO$<>"k" THEN 360
200 PRINT
205 PRINT "ENTER THE NUMBER OF ";
210 PRINT "ACTIVITIES IN THIS NETWORK";
215 INPUT M%:EE=9
220 FOR I=1 TO M%
225 PRINT
230 PRINT USING "-----------ACTIVITY ## -----------"; I
231 PRINT "CODE (LETTER E.G. A),","DESCRIPTION(TITLE)"
232 INPUT A$(I,1),A$(I,2)
235 PRINT "ENTER START NODE, END NODE";
240 INPUT A(I,1),A(I,2)
245 IF A(I,2)<=A(I,1) THEN 255 ELSE 280
255 PRINT "START NODE MUST BE NUMBERED LOWER";
260 PRINT "  THEN END NODE, AND END NODE MUST"
265 PRINT "  BE LESS THAN THE NUMBER OF ACTIVITIES.";
270 PRINT "      *** TRY ENTRY AGAIN ***"
275 GOTO 225
280 PRINT "ENTER THREE TIME ESTIMATES";
285 PRINT " FOR THIS ACTIVITY (BEST,LIKELY,WORST)";
290 INPUT A(I,3),A(I,4),A(I,5)
300 NEXT I
350  GOTO 460
360 IF IO$<>"D" AND IO$<>"d" THEN 470
370 CLS:LOCATE 5,30:PRINT "DISK INPUT ROUTINE"
375 ON ERROR GOTO 15000
380 : PRINT:LINE INPUT "Name of disk file, e.g., B:xxxxxxx.yyy  ";           B$:OPEN "I",1,B$
400 INPUT#1, M%,EE
410 FOR I= 1 TO M%
420 INPUT#1, A$(I,1),A$(I,2),A(I,1), A(I,2), A(I,3),A(I,4),A(I,5)
430 IF A$(I,1)="END" THEN 540
440 NEXT I
450 CLOSE #1
460 GOTO 530
470 IF IO$<>"R" AND IO$<>"r" THEN 130
530 ' Verify the data which was entered.
540 M=M%: TP=0:FOR I = 1 TO M: IF A(I,2)>TP THEN TP=A(I,2)
550 NEXT I: EE= TP
560 FOR I=1 TO M-1
570 FOR J=I+1 TO M
580 IF A(I,1)<=A(J,1) THEN 610
590 FOR K=1 TO 11: SV(K)=A(I,K):A(I,K)=A(J,K):A(J,K)=SV(K):NEXT K
600 FOR K=1 TO 2: SV$(K)=A$(I,K):A$(I,K)=A$(J,K):A$(J,K)=SV$(K):NEXT K
610 NEXT J,I
620 CLS
630 XX=5: COLOR 14,1,1:CLS:PRINT TAB(25);"VERIFICATION OF INPUT":PRINT
640 Z1$="Leg Description   Expected  Early   Early    Last    Last   Slack"
650 Z2$="                    Time     Start    Fin.    Start   Fin.   Time"
660 Z3$="    Leg     Description    Begin   End     Optimist  Likely  Pessimist"
670 Z4$="                           Event   Event     Time     Time      Time  "
680 PRINT "No. ";Z3$
690 PRINT "    ";Z4$
700 K=0
710 C4$="###      "
720 FOR I=1 TO M%
730 PRINT USING C4$;I;
740 C1$="  ####  "
750 C2$="\       \":C3$="  ####.## "
760 PRINT USING C2$;A$(I,1),A$(I,2);
770 FOR J=1 TO 2
780 PRINT USING C1$;A(I,J);:NEXT J
790 FOR J=3 TO 5
800 PRINT USING C3$;A(I,J);:NEXT J
810 K=K+1: IF K>=20 THEN MSG$ ="Enter to Page":LGTH=1:ANSWER$="":GOSUB 10000
820 PRINT: NEXT I
830 COLOR 14,1,1:  MSG$= "Please input a -2 to add more,-1 to continue, or number to change ":LGTH=2:GOSUB 10000:ANSWER=VAL(ANSWER$):L=ANSWER:IF L=-1 THEN 905
840 IF L<>-2 THEN 860
850 L=M%+1: M%=L:N%=N%+1:XCHECK=1:GOTO 860
860 IF L>M% THEN GOTO 830: ELSE GOSUB 20000:IF ABORT GOTO 830
865 INPUT "NEW LEG LETTER OR END TO ABORT  ";A$(L,1)
867 IF A$(L,1)="END" THEN GOSUB 22000:GOTO 830
870 INPUT "New Description  ";A$(L,2): INPUT  "New Beginning Event Number  ";              A(L,1)
880 INPUT "New Ending Event Number"; A(L,2):INPUT "New Optimistic Finish  ";A(L,3)
890 INPUT "New Likely Finish  ";A(L,4):INPUT "New Pess. Finish  ";A(L,5)
900 GOTO 620
905 MSG$ ="Want hard copy of your data  ":LGTH=1:GOSUB 10000:P$=ANSWER$:            IF P$<>"Y" AND P$<>"y" THEN 1040
910 LOCATE 25,3:INPUT"TITLE ";T$:LPRINT "INPUT:  ";T$
920 LPRINT"DATE: ";DATE$
930 LPRINT "NO "; Z3$
940 LPRINT "   "; Z4$
950 FOR I=1 TO M%
960 LPRINT USING C4$;I;
970 LPRINT USING C2$;A$(I,1),A$(I,2);
980 FOR J=1 TO 2
990 LPRINT USING C1$;A(I,J);:NEXT J
1000 FOR J=3 TO 5
1010 LPRINT USING C3$;A(I,J);:NEXT J
1020 LPRINT " "
1030 NEXT I
1040 MSG$="HIT ANY KEY TO COMPUTE RESULTS":LGTH=1:GOSUB 10000
1045 LOCATE 25,2: PRINT "WAIT.....";
1050 FOR I=1 TO M%
1060 A(I,6)=(A(I,3)+4*A(I,4)+A(I,5))/6
1070 IF A(I,1)=1 THEN A(I,7)=0:A(I,8)=A(I,6):GOTO 1110
1080 MAX = 0.
1090 FOR J= 1 TO M%
1092 IF A(J,2)<> A(I,1) THEN 1098
1094 IF A(J,8)> MAX THEN MAX = A (J,8)
1096 A(I,7)=MAX
1098 NEXT J
1100 A(I,8)=A(I,7)+A(I,6)
1110 NEXT I
1120 ' Backward pass?
1130 XM = 0.
1140 FOR I= M% TO 1 STEP -1
1150 IF A(I,2)<>EE THEN 1170
1160 IF XM<A(I,8) THEN XM = A(I,8)
1170 NEXT I
1180 FOR I=M% TO 1 STEP -1
1190 IF A(I,2)= EE THEN A(I,10)= XM: GOTO 1260
1200 MIM =999999.
1210 FOR J= M% TO 1 STEP -1
1220 IF A(I,2)<>A(J,1) THEN 1250
1230 IF A(J,9)<MIM THEN MIM =A(J,9)
1240 A(I,10)=MIM
1250 NEXT J
1260 A(I,9)=A(I,10)-A(I,6)
1270 NEXT I
1280 ' Slack Variable Calculations
1290 FOR I=1 TO M%
1300 A(I,11)=A(I,10)-A(I,8)
1310 NEXT I
1320 CLS:K=0        'PRINT OUTPUT
1325 COLOR 14,1,1:CLS:PRINT TAB(35);"RESULTS":COLOR 15,1,1:PRINT TAB(27)"CRITICAL PATH IN WHITE":COLOR 14,1,1
1330 PRINT "Code   Description Expected  Early   Early   Last   Last   Slack"
1340 PRINT "                    Time     Start   Fin.    Start  Fin.    Time"
1350 C5$=" ###.## "
1360 FOR I=1 TO M%
1365 IF A(I,11)=0 THEN COLOR 15 ELSE COLOR 14
1370 PRINT USING C2$;A$(I,1),A$(I,2);
1380 FOR J=6 TO 11
1390 PRINT USING C5$;A(I,J);:NEXT J
1400 PRINT:K=K+1:IF K=20 THEN MSG$="Enter to Page  ":LGTH=1:GOSUB 10000:ANSWER$      =DU$:K=0
1410 NEXT I
1420 MSG$="Hard Copy of Results  (Y/N)  ":LGTH=1:GOSUB 10000:P$=ANSWER$:               IF P$<>"y" AND P$<>"Y" THEN 1490
1425 LPRINT:LPRINT "OUTPUT:  ";T$
1430 LPRINT " ":LPRINT Z1$:LPRINT Z2$
1440 FOR I= 1 TO M%
1450 LPRINT A$(I,1);"   ";:LPRINT USING C2$;A$(I,2);
1460 FOR J=6 TO 11
1470 LPRINT USING C5$;A(I,J);:NEXT J
1480 LPRINT "   ":NEXT I
1490 CLS: COLOR 14,1,1:LOCATE 5,30: PRINT "OUTPUT MENU"
1500 COLOR 14,1,1:LOCATE 7,20:PRINT "CP - CRITICAL PATH AND TIME"
1510 LOCATE 8,20: PRINT "S - SAVE"
1520 LOCATE 9,20: PRINT "E - END "
1530 LOCATE 10,20: PRINT "C - CHANGE"
1535 LOCATE 11,20:PRINT"N - NEW CASE"
1540 LOCATE 20,35:  INPUT "Choice, please...  ";OP$:IF OP$="C"             OR OP$="c" THEN 530
1550 IF OP$<>"CP" AND OP$<>"cp" THEN 1710
1560 'Identification of critical path and costs
1570 CO=0:PATH$=" ":SI=0
1580 FOR I=1 TO M%: IF A(I,11)>0.000001 THEN 1600
1590 CO=CO+A(I,6):PATH$=PATH$+" "+A$(I,1):SI=SI+((A(I,3)-A(I,5))/6)^2
1600 NEXT I:C6$="###,###,###.##":SI=SQR(SI)
1610 CLS:LOCATE 5,30:COLOR 14,1,1:PRINT "CRITICAL PATH":PRINT PATH$:PRINT
1620 PRINT "Time of Critical Path  ":PRINT USING C6$;CO:INPUT "Scheduled Project Time (use same time units as data)   ";ST:Z=(ST-CO)/SI:XX=0
1630 BB$="Probability of being completed on time ": IF Z<0 THEN 1670
1640 A=0.4361836:B=-0.1201676:C=0.937298: D=(2.7182818#^(-Z^2/2))*(2*3.1415926#)^            (-0.5):E=(1+0.3326*Z)^(-1):P=1.#-D*(A*E+B*E^2+C*E^3):IF XX>0 THEN 1660
1650 COLOR 14,1,1:PRINT USING "Z= ##.###    ";Z;:PRINT BB$;:PRINT USING "##.#%";P*100:              GOTO 1680
1660 PRINT USING "Z= ##.### ";-Z;:PRINT BB$;:PRINT USING "##.#%";(1-P)*100:           GOTO 1680
1670 XX=99: Z=-Z:GOTO 1640
1680 MSG$="Hard Copy (Y/N) ":LGTH=1:GOSUB 10000:P$=ANSWER$:                                   IF P$<>"y" AND P$<>"Y" THEN 1490
1690 LPRINT " ":LPRINT "CRITICAL PATH":LPRINT PATH$:LPRINT " ":LPRINT "Scheduled project time is  ";ST
1700 LPRINT "Time of critical path ":LPRINT USING C6$;CO: IF XX=0 THEN LPRINT "Z= ";         Z;"   ";BB$;P:GOTO 1490
1705 GOTO 1490
1710 IF OP$="E" AND OP$="e" THEN END
1715 IF OP$="N" OR OP$="n" THEN 130
1720 IF OP$<>"S" AND OP$<>"s" THEN 1770
1730 LINE INPUT "Name of disk:file ";X$:OPEN "O",1,X$
1740 PRINT#1,M%,EE
1750 FOR I=1 TO M%:PRINT#1,CHR$(34);A$(I,1);CHR$(34);",";CHR$(34);A$(I,2);              CHR$(34);A(I,1);A(I,2);A(I,3);A(I,4);A(I,5):NEXT I
1760 CLOSE #1: GOTO 1490
1770 SCREEN 0,0,0:WIDTH 80:COLOR 14,1,1:CLS:PRINT "ENDING...";:                FOR I=1 TO 1000:NEXT: KEY ON:END
10000 REM This subroutine saves the cursor position as ONCE (Col. or Y)
10010 REM and WAS (Row or X), locates the cursor on the 25th status line
10020 REM gets an input value (string only), and returns the cursor to
10030 REM where it ONCE WAS.
10040 REM             Input Variables = MSG$;Output = ANSWER$
10050 REM                               LGTH of desired ANSWER
10060 ' Store cursor position
10070   ONCE = CSRLIN
10080   WAS  = POS(0)
10085 ANSWER$=""        'empty the answer string
10090 ' Make sure softkeys are off, locate cursor on 25th line
10100   KEY OFF
10110   LOCATE 25,3
10120 ' Print the message the user requests and get an input
10130   PRINT MSG$;:ANSWER$=INPUT$(LGTH)
10150 ' Clear off the line for future use
10160   LOCATE 25,1:PRINT SPC(79);
10170 ' Restore the cursor to where it was
10180   LOCATE ONCE, WAS
10190   RETURN
15000 REM This subroutine does some error-checking, for instance, for the
15010 REM open statement in line 390
15020 REM It requires a ON ERROR GOTO 15000 statement at front of program
15025 MISTEAK=0:WHEREAT=0
15030 MISTEAK = ERR: WHEREAT=ERL
15040 IF MISTEAK = 55 THEN CLOSE:RESUME
15050 IF MISTEAK = 53 THEN PRINT"CAN'T FIND THAT FILE - CATALOG: ":PRINT      "A:":FILES "A:*.*":PRINT "B:":FILES "B:*.*":PRINT:PRINT"WAIT...":       FOR I= 1 TO 7000:NEXT I:RESUME 130
15060 IF MISTEAK = 61 THEN PRINT"DISK IS FULL - PLEASE GET ANOTHER:FOR I=1 TO         3000:NEXT I:RESUME 1730
15070 IF MISTEAK = 64 THEN PRINT"THAT'S NOT A GOOD FILE NAME-USE B:XXXXXXX.YYY":            FOR I=1 TO 3000:NEXT I:RESUME 130
15075 IF MISTEAK = 68 THEN PRINT "THAT DISK DOESN'T EXIST...USE A: OR B:":              FOR I=1 TO 3000:NEXT I: RESUME 130
15080 IF MISTEAK = 70 THEN PRINT "THE DISK IS WRITE-PROTECTED - CAN'T USE IT":          FOR I=1 TO 3000:NEXT I:RESUME 130
15090 IF MISTEAK = 71 THEN PRINT "CLOSE THE DISK DOOR":FOR I=1 TO 3000:NEXT I:          RESUME 130
15100 IF MISTEAK =72 THEN PRINT "I THINK THE DISK IS BAD":TRIES=TRIES+1:                IF TRIES<3 THEN RESUME ERL: ELSE PRINT "ENDING...CHECK DISK":                   FOR I=1 TO 3000:NEXT I:GOTO 1770
15105 IF MISTEAK =67 THEN PRINT "YOU HAVE TOO MANY FILES OPEN AT ONE TIME":             FOR I=1 TO 3000:NEXT I: RESUME 130
15110 ON ERROR GOTO 0: RESUME
18050 PRINT      "A:":FILES "A:*.*":PRINT "B:":FILES "B:*.*":PRINT:PRINT"WAIT...":       FOR I= 1 TO 7000:NEXT I:GOTO 175
20000 REM This subroutine switches the active and visual pages in SCREEN 0
20010 REM mode to page 1, thus allowing the programmer to put "help"
20020 REM material, ancillary input, or other messages on a "clean slate."
20022 ONCE = CSRLIN: WAS =POS(0)
20025 ABORT = 0
20030 SCREEN 0,1,1,1    'color burst, apage=1,vpage=1
20032 CLS
20035 KEY (1) ON
20037 ON KEY(1) GOSUB 22000
20040 LOCATE 25,1::  PRINT "Hit <F1> key BEFORE entering to abort - I'll WAIT before going on";
20042 LOCATE 5,35:PRINT "CHANGE ROUTINE"
20050 FOR I=1 TO 5000:NEXT I
20070 RETURN
22000 CLS:SCREEN 0,1,0,0
22010 ABORT=1
22015 IF ABORT AND XCHECK THEN M%=M%-1:N%=N%-1:XCHECK=0
22020 LOCATE ONCE,WAS
22030 RETURN
30000 GOTO 20070
30010 RETURN
```

## PRGTIM.BAS

```bas
10 '**************************************************************************
20 '*                            PRGTIM.BAS                                  *
30 '*           BASIC source file for PRGTIM.EXE  (Revised 05/30/84)         *
40 '**************************************************************************
50 '*                                                                        *
60 '*    This program calculates amount of time spent by an individual using *
70 '* an application program, and then writes that information, along with   *
80 '* the date, & user's name, to a file, `PRGTIM.DTA'.  The file also shows *
90 '* how much of the use-time was for business purposes.                    *
100 '*                                                                        *
110 '*   This program works in conjunction with `TMSTRT.BAS,' as part of a    *
120 '* BATCH file, to create a record of computer use by various users.       *
130 '*                                                                        *
140 '*   The program `RPTUSE.BAS' will print out a REPORT using the data in   *
150 '* the file `PRGTIM.DTA' for documentation of PC use.                     *
160 '*                                                                        *
170 '*   This REVISION defaults to `BUSINESS USE' when data is entered, but   *
180 '* it will write the files EXACTLY as before, and it can be used with     *
190 '* PRGTIM.DTA files written with the previous version.                    *
200 '*                                                                        *
210 '*   PLEASE see `USE-TIME.DOC' for instructions on using this program     *
220 '*                                                                        *
230 '*       Program written by Steve Kent, Marin/Sonoma-PC User's Group      *
240 '*       CIS 72406,504                       v 1.2  /  05/30/84           *
250 '**************************************************************************
260 '
270 KEY OFF : CLS : DEFINT C,R
280 DEF SEG=&H40 : POKE &H17 , PEEK (&H17) OR 64 'Set for Capital letters
290 HRSTOP=VAL(MID$(TIME$,1,2)) : MINSTOP=VAL(MID$(TIME$,4,2))
300 STOPTIME = HRSTOP*60 + MINSTOP
310 OPEN "TMSTRT.DTA" FOR INPUT AS #1
320 INPUT #1, DAT$, STARTTIME$, STARTTIME
330 IF STOPTIME < STARTTIME THEN STOPTIME = STOPTIME + (24*60)
340 LOCATE 2,36 : COLOR 0,7: PRINT " "+DAT$+" ": COLOR 7,0
350 LOCATE 25,1: V$=" Please note: COMMAS are NOT permitted in data entries " :     PRINT SPACE$((80-LEN(V$))*.5); : COLOR 0,7: PRINT V$;: COLOR 7,0
360 GOSUB 820
370 LOCATE 18,10 : PRINT "Your NAME, INITIALS, or other ID (Max=11 char.): "
380 LOCATE 18,59: PRINT STRING$(11,"_"): LOCATE 18,58: INPUT " ",NM$
390 IF NM$="" THEN LOCATE 18,3: BEEP : PRINT SPACE$(74) : GOTO 370
400 IF LEN(NM$) > 11 THEN 410 ELSE 420
410 LOCATE 22,24: BEEP: PRINT "Too many characters.  Limit is 11" : GOSUB 800:      GOSUB 820 : GOTO 370
420 LOCATE 5,1 : V$=" NAME: " + NM$ +" ": PRINT SPACE$((80-LEN(V$))*.5);
430 COLOR 0,7 : PRINT V$ : COLOR 7,0 :LOCATE 18,3 :PRINT SPACE$(74)
440 LOCATE 18,11: PRINT "NAME of PROGRAM you were using (Max=12 char): "
450 LOCATE 18,57: PRINT STRING$(12,"_"): LOCATE 18,56: INPUT " ",PN$
460 IF PN$ ="" THEN LOCATE 18,3: BEEP : PRINT SPACE$(74) : GOTO 440
470 IF LEN(PN$) > 12 THEN 480 ELSE 490
480 BEEP : LOCATE 22,24: PRINT "Too many characters.  Limit is 12" : GOSUB 800:     GOSUB 820 : GOTO 440
490 LOCATE 8,1 : V$=" PROGRAM: " + PN$ +" ": PRINT SPACE$((80-LEN(V$))*.5);
500 COLOR 0,7 : PRINT V$ : COLOR 7,0 :LOCATE 20,3 :PRINT SPACE$(74)
510 LOCATE 18,7: PRINT "COMMENTS (Max=24 char.  Hit <enter> to SKIP): "
520 LOCATE 18,53: PRINT STRING$(24,"_"): LOCATE 18,52: INPUT " ",CM$
530 IF LEN(CM$) > 24 THEN 540 ELSE 550
540 BEEP: LOCATE 22,24: PRINT "Too many characters.  Limit is 24" : GOSUB 800:      GOSUB 820 : GOTO 510
550 LOCATE 11,1 : V$=" COMMENTS: " + CM$ +" ": PRINT SPACE$((80-LEN(V$))*.5);
560 COLOR 0,7 : PRINT V$ : COLOR 7,0 :LOCATE 20,3 :PRINT SPACE$(74)
570 ELAPSTIME = STOPTIME - STARTTIME : ELAPSHRS = ELAPSTIME \ 60 :                  XTRAMIN = ELAPSTIME MOD 60
580 ELAPSTIM$=STR$(ELAPSHRS) + " Hrs."+STR$(XTRAMIN) +" Min."
590 ELAPSTIME$=STR$(ELAPSHRS) + ":" + STR$(XTRAMIN)
600 LOCATE 15,1 :  V$="Elapsed time for " :  Y$=": "
610 PRINT SPACE$((80-LEN(V$+PN$+Y$+ELAPSTIM$))*.5);: COLOR 0,7:                     PRINT V$+PN$+Y$+ELAPSTIM$ : COLOR 7,0
620 LOCATE 18,3 : PRINT SPACE$(74)
630 LOCATE 25,5 : PRINT SPACE$(70);
640 LOCATE 17,14:PRINT "How much of this time was for NON-BUSINESS purposes?"
660 LOCATE 18,32 : PRINT "Hrs. <enter>" : LOCATE 18,50 : PRINT "Min. <enter>"
670 LOCATE 21,1 : V$="Hitting <enter> twice" : PRINT SPACE$((80-LEN(V$))*.5)+V$
680 LOCATE 22,1 : V$="credits ALL of the use-time" :                                PRINT SPACE$((80-LEN(V$))*.5)+V$
690 LOCATE 23,1 : V$="as `BUSINESS USE'" : PRINT SPACE$((80-LEN(V$))*.5)+V$
700 LOCATE 18,28 :INPUT" ",NONBH : LOCATE 18,46 : INPUT " ",NONBMIN
710 IF (NONBH*60+NONBMIN)>ELAPSTIME OR NONBMIN > 60 THEN 720 ELSE 740
720 LOCATE 19,27: BEEP : PRINT "Please redo last input...."
730 SOUND 32767,8: SOUND 32767,1: FOR R=17 TO 19: LOCATE R,1: PRINT SPACE$(79):     NEXT: GOTO 660
740 NONBSNS=NONBH*60+NONBMIN : BSNS=ELAPSTIME-NONBSNS
750 '
760 OPEN "PRGTIM.DTA" FOR APPEND AS #2
770 WRITE #2, DAT$, STARTTIME$, NM$, PN$, CM$, BSNS , NONBSNS
780 CLS : END: 'SYSTEM
790 '*********** SUBROUTINE: PAUSE AND ERASE LINES *************
800 SOUND 32767,16: SOUND 32767,1: FOR R=18 TO 22: LOCATE R,3:                      PRINT SPACE$(74):  NEXT: RETURN
810 '*********** SUBROUTINE: DRAWING BOX AROUND QUERIES *************
820 LOCATE 16,2:PRINT CHR$(218):FOR C=3 TO 77 : LOCATE 16,C:PRINT CHR$(196):        NEXT : LOCATE 16,78:PRINT CHR$(191)
830 FOR R=17 TO 18 : LOCATE R,2:PRINT CHR$(179): LOCATE R,78 : PRINT CHR$(179):     NEXT
840 LOCATE 19,2:PRINT CHR$(192):FOR C=3 TO 77 : LOCATE 19,C:PRINT CHR$(196):        NEXT : LOCATE 19,78:PRINT CHR$(217)
850 RETURN
```

## RPTUSE.BAS

```bas
100 '**************************************************************************
110 '*                           RPTUSE.BAS                                   *
120 '*                                                                        *
130 '*      This program READS data from the file, `PRGTIM.DTA', and then     *
140 '*   prints out a report showing a record of PC use, listing the DATE,    *
150 '*   START TIME, USER'S ID, and amount of BUSINESS & NON-BUSINESS USE.    *
160 '*                                                                        *
170 '*      The program will print the report ON THE SCREEN and it will       *
180 '*   also give a HARDCOPY PRINTOUT, if your printer is ONLINE.            *
190 '*                                                                        *
200 '*          Program written by Steve Kent, CIS # 72406,504                *
210 '*                      v 1.1      05/03/84                               *
220 '**************************************************************************
230 KEY OFF : CLS : OPEN "PRGTIM.DTA" FOR INPUT AS #1
240  LOCATE 10,1: V$="Program is set to print to SCREEN and to PRINTER" :            PRINT SPACE$((80-LEN(V$))*.5)+V$
250  LOCATE 13,1 : V$="In order to get the HARDCOPY PRINTOUT," :                     PRINT SPACE$((80-LEN(V$))*.5)+V$
260  LOCATE 14,1 : V$="TURN ON PRINTER NOW, and put it ONLINE." :                    PRINT SPACE$((80-LEN(V$))*.5)+V$
270 LOCATE 20,20 : PRINT " Hit any key to continue, when ready...." :I$=INKEY$ :    IF I$="" THEN 270 ELSE CLS
280 PRINT: PRINT: V$="REPORT OF P.C. USE":PRINT SPACE$((80-LEN(V$))*.5)+V$
290 LPRINT:LPRINT: V$="REPORT OF P.C. USE":LPRINT SPACE$((80-LEN(V$))*.5)+V$
300 PRINT STRING$(79,"-") : PRINT TAB(10);"STRT";: PRINT TAB(31);"NAME OF";:        PRINT TAB(69);"BUSNS";: PRINT TAB(75);"PRSNL"
310 LPRINT STRING$(79,"-") : LPRINT TAB(10);"STRT";: LPRINT TAB(31);"NAME OF";:     LPRINT TAB(69);"BUSNS";: LPRINT TAB(75);"PRSNL"
320 PRINT TAB(3); "DATE";: PRINT TAB(10); "TIME";: PRINT TAB(18); "USER ID";:       PRINT TAB(31); "PROGRAM";
330 PRINT TAB(51);"COMMENTS";: PRINT TAB(69);"HR:MN";: PRINT TAB(75);"HR:MN"
340 LPRINT TAB(3); "DATE";: LPRINT TAB(10); "TIME";: LPRINT TAB(18); "USER ID";:    LPRINT TAB(31); "PROGRAM";
350 LPRINT TAB(51);"COMMENTS";: LPRINT TAB(69);"HR:MN";: LPRINT TAB(75);"HR:MN"
360 PRINT STRING$(79,"=") : LINENUM=7
370 LPRINT STRING$(79,"=") : LINENUM=7
380 '*************************************************************************
390 INPUT #1, DAT$, STARTTIME$, NM$, PN$, CM$, BSNS, NONBSNS
400 YR$=RIGHT$(DAT$,2) : DT$=MID$(DAT$,1,6)+YR$
410 START$=MID$(STARTTIME$,8,2)+MID$(STARTTIME$,11,2)
420 BH=BSNS\60:BMIN=BSNS MOD 60 : NBH=NONBSNS\60: NBMIN=NONBSNS MOD 60
430 SBM$=STR$(BMIN):BMIN$=MID$(SBM$,2,(LEN(SBM$)-1)) : IF LEN(BMIN$)=1 THEN         BMIN$="0"+BMIN$
440 SNBM$=STR$(NBMIN) : NBMIN$=MID$(SNBM$,2,(LEN(SNBM$)-1)) :                       IF LEN(NBMIN$)=1 THEN NBMIN$="0"+NBMIN$
450 TBSNS=TBSNS+BSNS : TBH=TBSNS\60 : TBM=TBSNS MOD 60
460 SBM$=STR$(TBM): TBM$=MID$(SBM$,2,(LEN(SBM$)-1)) : IF LEN(TBM$)=1 THEN           TBM$="0"+TBM$
470 TNBSNS=TNBSNS+NONBSNS : TNBH=TNBSNS\60 : TNBM=TNBSNS MOD 60
480 SNBM$=STR$(TNBM): TNBM$=MID$(SNBM$,2,(LEN(SNBM$)-1)) :IF LEN(TNBM$)=1 THEN      TNBM$="0"+TNBM$
490 PRINT DT$;: PRINT TAB(10);START$;: PRINT TAB(16);NM$;: PRINT TAB(29);PN$;:      PRINT TAB(43);CM$;:
500 PRINT TAB(69);:PRINT USING "##";BH;:PRINT ":"+BMIN$;: PRINT TAB(75);:           PRINT USING "##"; NBH;:PRINT ":"+NBMIN$
510 LPRINT DT$;: LPRINT TAB(10);START$;: LPRINT TAB(16);NM$;:                       LPRINT TAB(29);PN$;:  LPRINT TAB(43);CM$;:
520 LPRINT TAB(69);:LPRINT USING "##";BH;:LPRINT ":"+BMIN$;: LPRINT TAB(75);:       LPRINT USING "##"; NBH;:LPRINT ":"+NBMIN$
530 IF EOF(1) THEN CLOSE :GOTO 570
540 LINENUM=LINENUM+1 : IF LINENUM < 56 THEN 390
550 LPRINT :LPRINT :LPRINT : PGNUM =PGNUM + 1
560 LPRINT TAB(39); "-"; PGNUM; "-" : LPRINT CHR$(12): LINENUM=0: GOTO 280
570 PRINT TAB(69);STRING$(11,"=")
580 LPRINT TAB(69);STRING$(11,"=")
590 PRINT : PRINT TAB(45);"TOTAL BUSINESS USE = " ;: PRINT TAB(66);
600 PRINT USING "#####"; TBH;: PRINT TAB(71); ":" + TBM$ : PRINT
610 LPRINT :LPRINT TAB(45);"TOTAL BUSINESS USE = " ;: LPRINT TAB(66);
620 LPRINT USING "#####";TBH;: LPRINT TAB(71); ":" + TBM$ :LPRINT
630 PRINT TAB(51);"TOTAL PERSONAL USE = " ;: PRINT TAB(72);:PRINT USING "#####";    TNBH;: PRINT TAB(77); ":" + TNBM$
640 LPRINT TAB(51);"TOTAL PERSONAL USE = " ;: LPRINT TAB(72);:                      LPRINT USING"#####"; TNBH;: LPRINT TAB(77); ":" + TNBM$
650 KEY ON : END
```

## TAXDEDCT.BAS

```bas
1000 '>>>THIS PROGRAM RECORDS INCOME TAX DEDUCTIONS
1100 '>>>HARRY G. FRIEDMAN
1200 '>>>945 Dudley Drive
1300 '>>>Shreveport, LA 71104
1400 '>>>v 1.0
1500 '
1600 '>>>Filename=TAXDEDCT.BAS
1700 '>>>DATA FILE IS RANDOM ACCESS.
1800 '>>>CODING SCHEME IS:
1900 '>>>       01/XX - Contributions
2000 '>>>       02/XX - Medical
2100 '>>>       03/XX - Interest
2200 '>>>       04/XX - Taxes
2300 '>>>THE XX PORTION OF THE CODE IS ASSIGNED TO THE PAYEE IN NUMERICAL
2400 '>>>SEQUENCE, ONE CODE NUMBER FOR EACH INDIVIDUAL PAYEE.
2500 '>>>DATES AND CODES ARE ENTERED WITHOUT "/" - AMOUNTS WITH ONLY THE
2600 '>>>DECIMAL POINT (.).
2700 '>>>MENU ITEM 6 PRINTS A LIST AND TOTAL IN DATA ENTRY FORMAT.
2800 '>>>MENU ITEM 7 PRINTS A LIST AND TOTALS SORTED BY CATAGORY AND PAYEE.
2900 '
3000 '>>>Permission is hereby granted for the unlimited use or reproduction
3100 '>>>of this program.
3200 '>>>Notification of changes or additions will be appreciated.
3300 'FILENAME=TAXDEDCT - DATA FILENAME=TAXDED.DAT
3400 KEY OFF:CLS
3500 DEFINT I
3600 OPTION BASE 1
3700 DAT=250
3800 DIM REC$(DAT)
3900 OPEN "B:TAXDED.DAT" AS #1 LEN=64
4000 FIELD #1,1 AS US$,6 AS DTE$,4 AS CDE$,45 AS PAY$,8 AS AMT$
4100 FIELD #1,64 AS RECORD$
4200 '
4300 '>>>***<<<
4400 '
4500 PRINT TAB(40) "MENU"
4600 PRINT
4700 PRINT TAB(30)1; "INITIALIZE the FILE"
4800 PRINT TAB(30)2; "CREATE or ADD a RECORD"
4900 PRINT TAB(30)3; "DISPLAY a RECORD"
5000 PRINT TAB(30)4; "EDIT a RECORD"
5100 PRINT TAB(30)5; "DELETE a RECORD"
5200 PRINT TAB(30)6; "PRINT HARDCOPY"
5300 PRINT TAB(30)7; "SORT and PRINT"
5400 PRINT TAB(30)8; "EXIT - RETURN to BASIC"
5500 PRINT:INPUT "SELECT FUNCTION ",ISELCT
5600 IF (ISELCT<1) OR (ISELCT>8) THEN PRINT "BAD SELECTION - PLEASE REENTER":        GOTO 5500
5700 ON ISELCT GOSUB 6000,7500,11000,12200,15400,17100,18900,24700
5800 GOTO 4500
5900 '
6000 '>>>INITIALIZE FILE ROUTINE<<<
6100 '
6200 INPUT "ARE YOU SURE";ANS$:IF ANS$<>"Y" THEN RETURN
6300 LSET RECORD$=CHR$(255)
6400 FOR I=1 TO 250
6500 PUT #1,I
6600 NEXT
6700 RETURN
6800 '
6900 '>>>SEQUENCE NUMBER ROUTINE<<<
7000 '
7100 INPUT "SEQUENCE NUMBER ",ISEQ
7200 IF (ISEQ<1) OR (ISEQ>250) THEN PRINT "BAD SEQUENCE NUMBER-PLEASE REENTER":      GOTO 7100 ELSE GET #1,ISEQ
7300 IF USEFLG=1 THEN 8600 ELSE RETURN
7400 '
7500 '>>>FILE ENTRY ROUTINE<<<
7600 '
7700 USEFLG=0
7800 MODE$=""
7900 INPUT "CREATE THE FILE or ADD A RECORD? - REPLY 'C' or 'A' ",MODE$
8000 PRINT
8100 IF MODE$="C" THEN ISEQ=1:GOTO 9400 ELSE MODE$="A"
8200 INPUT "Is a deleted record SEQUENCE NUMBER to be reused? - Reply Y/N ",         ANS$:PRINT
8300 IF ANS$<>"Y" THEN 8800 ELSE USEFLG=1
8400 IF ASC(US$)<>255 THEN INPUT "OVERWRITE";X$:IF X$<>"Y" THEN 4500
8500 GOTO 6900
8600 PRINT:PRINT "Inserting record at SEQUENCE NUMBER";ISEQ:PRINT
8700 GOTO 9400
8800 PRINT:PRINT "Adding record to file.":PRINT
8900 ISEQ=1
9000 FOR X=1 TO LOF(1)/128
9100 GET #1,ISEQ
9200 IF DTE$<>"ZZZZZZ" THEN ISEQ=ISEQ+1 ELSE PRINT ISEQ;"is next SEQUENCE";          " NUMBER for ADD":GOTO 9400
9300 NEXT
9400 LSET US$=CHR$(0)
9500 INPUT "DATE -   ",CALENDAR$
9600 LSET DTE$=CALENDAR$
9700 INPUT "CODE -   ",CODE$
9800 LSET CDE$=CODE$
9900 INPUT "PAYEE -  ",PAYEE$
10000 LSET PAY$=PAYEE$
10100 INPUT "AMOUNT - ",AMOUNT$
10200 RSET AMT$=AMOUNT$
10300 PUT #1,ISEQ
10400 IF USEFLG=1 THEN USEFLG=0:GOTO 4500
10500 INPUT "MORE NEW DATA";ANS$:IF ANS$="Y" THEN ISEQ=ISEQ+1:GOTO 9400 ELSE         ISEQ=ISEQ+1:LSET DTE$="ZZZZZZ"
10600 LSET CDE$=CHR$(32):LSET PAY$=CHR$(32)
10700 LSET AMT$=CHR$(32)
10800 PUT #1,ISEQ:RETURN
10900 '
11000 '>>>DISPLAY ROUTINE<<<
11100 '
11200 GOSUB 6900
11300 PRINT "SEQUENCE NUMBER ",ISEQ
11400 PRINT LEFT$(DTE$,2)+"/"+MID$(DTE$,3,2)+"/"+RIGHT$(DTE$,2)
11500 PRINT LEFT$(CDE$,2)+"/"RIGHT$(CDE$,2)
11600 PRINT PAY$
11700 PRINT AMT$
11800 INPUT "MORE RECORDS FOR DISPLAY - Y/N or E";ANS$
11900 IF (ANS$<>"Y") AND (ANS$<>"N") AND (ANS$<>"E") THEN 11800
12000 IF (ANS$="Y") THEN 11000 ELSE IF (ANS$="N") THEN RETURN ELSE PRINT:             PRINT "NEXT EDIT"
12100 '
12200 '>>>FILE EDIT ROUTINE<<<
12300 '
12400 PRINT:GOSUB 6800
12500 PRINT TAB(28)"FIELD TO CHANGE MENU"
12600 PRINT
12700 PRINT TAB(30)1;"DATE"
12800 PRINT TAB(30)2;"CODE"
12900 PRINT TAB(30)3;"PAYEE"
13000 PRINT TAB(30)4;"AMOUNT"
13100 PRINT:INPUT "WHICH FIELD TO CHANGE";FLD
13200 IF (FLD<1) OR (FLD>4) THEN PRINT "WRONG FIELD NUMBER":GOTO 13100
13300 ON FLD GOSUB 13500,13900,14300,14700
13400 GOTO 12500
13500 PRINT "CURRENT RECORD IS ";DTE$
13600 INPUT "NEW DATE          ",CALENDAR$
13700 LSET DTE$=CALENDAR$
13800 GOTO 15000
13900 PRINT "CURRENT RECORD IS ";CDE$
14000 INPUT "NEW CODE          ",CODE$
14100 LSET CDE$=CODE$
14200 GOTO 15000
14300 PRINT "CURRENT RECORD IS ";PAY$
14400 INPUT "NEW PAYEE         ",PAYEE$
14500 LSET PAY$=PAYEE$
14600 GOTO 15000
14700 PRINT "CURRENT RECORD IS ";AMT$
14800 INPUT "NEW AMOUNT       ",AMOUNT$
14900 RSET AMT$=AMOUNT$
15000 INPUT "ANY MORE CHANGES";ANS$
15100 IF ANS$="Y" THEN 13100 ELSE PUT #1,ISEQ:GOSUB 11400
15200 GOTO 4500
15300 '
15400 '>>>DELETE RECORD ROUTINE<<<
15500 '
15600 GOSUB 6900
15700 PRINT "SEQUENCE NUMBER";ISEQ
15800 PRINT LEFT$(DTE$,2)+"/"+MID$(DTE$,3,2)+"/"+RIGHT$(DTE$,2)
15900 PRINT LEFT$(CDE$,2)+"/"+RIGHT$(CDE$,2)
16000 PRINT PAY$
16100 PRINT AMT$
16200 INPUT "IS THIS THE RECORD TO DELETE";ANS$:IF ANS$<>"Y" THEN 4500
16300 LSET DTE$=CHR$(32)
16400 LSET CDE$=CHR$(32)
16500 LSET PAY$=CHR$(32)
16600 LSET AMT$=CHR$(32)
16700 PUT #1,ISEQ
16800 PRINT "THIS RECORD DELETED   ";ISEQ
16900 INPUT "ANY MORE DELETIONS";ANS$:IF ANS$="Y" THEN 15400 ELSE RETURN
17000 '
17100 '>>>HARDCOPY ROUTINE<<<
17200 '
17300 TOT=0
17400 LINCNT=0
17500 PRINT
17600 PRINT TAB(25):COLOR 1
17700 PRINT TAB(25)"PRINTING OUT DATA IN ENTRY SEQUENCE":COLOR 7:PRINT
17800 LPRINT TAB(62)"DATE   ";DATE$:LPRINT
17900 LPRINT "SEQ";TAB(8)"DATE";TAB(16)"CODE";TAB(41)"PAYEE";TAB(73)"AMOUNT"
18000 LPRINT "===";TAB(8)"====";TAB(16)"====";TAB(41)"=====";TAB(73)"======"
18100 LINCNT=LINCNT+4
18200 ISEQ=1
18300 GET #1,ISEQ
18400 LPRINT ISEQ;TAB(6)DTE$;TAB(16)CDE$;TAB(23)PAY$;TAB(71)AMT$
18500 TOT=TOT+VAL(AMT$)
18600 LINCNT=LINCNT+1:IF LINCNT=>58 THEN LPRINT CHR$(12):LINCNT=0:ELSE                GOTO 18700
18700 IF DTE$<>"ZZZZZZ" THEN ISEQ=ISEQ+1:GOTO 18300 ELSE LPRINT TAB(71)TOT:           LPRINT CHR$(12):GOTO 4500
18800 '
18900 '>>>SORT ROUTINE<<<
19000 '
19100 ISEQ=1
19200 FOR S=1 TO DAT
19300 GET #1,ISEQ
19400 REC$(S)=INPUT$(64,#1)
19500 IF ASC(US$)=0 OR ASC(US$)=32 THEN ISEQ=ISEQ+1:GOTO 19600 ELSE GOTO 19700
19600 NEXT
19700 COLOR 16,7:PRINT "SORT IN PROGRESS ";TIME$;:COLOR 7,0
19800 D=S:FLAG=0
19900 D=INT((D+1)/2)
20000 FOR Q=1 TO S-D
20100 IF MID$(REC$(Q),8,4)+MID$(REC$(Q),2,6)<=MID$(REC$(Q+D),8,4)+MID$(REC$           (Q+D),2,6) THEN 20200 ELSE SWAP REC$(Q),REC$(Q+D):FLAG=1
20200 NEXT
20300 IF FLAG=1 THEN FLAG=0:GOTO 20000
20400 IF D>1 THEN 19900
20500 PRINT:COLOR 0,7:PRINT "SORT COMPLETED   ";TIME$;:COLOR 7,0:PRINT
20600 COLOR 7,0
20700 '
20800 '>>>PRINT ROUTINE<<<
20900 '
21000 PRINT TAB(30):COLOR 1
21100 PRINT TAB(30)"PRINTING SORTED DATA":COLOR 7:PRINT
21200 LINCNT=0
21300 LPRINT TAB(20)"INCOME TAX DEDUCTIONS SORTED BY CATAGORY"
21400 LPRINT TAB(62)"DATE  ";DATE$:LPRINT
21500 LINCNT=LINCNT+2
21600 LPRINT "                       DEDUCTIONS              CODES"
21700 LPRINT "                       ==========              ====="
21800 LPRINT "                     Contributions             01/XX"
21900 LPRINT "                     Medical                   02/XX"
22000 LPRINT "                     Interest                  03/XX"
22100 LPRINT "                     Taxes                     04/XX"
22200 LPRINT "                    ================================"
22300 LPRINT TAB(3)"DATE";TAB(12)"CODE";TAB(41)"PAYEE";TAB(73)"AMOUNT"
22400 LPRINT TAB(3)"====";TAB(12)"====";TAB(41)"=====";TAB(73)"======"
22500 LINCNT=LINCNT+10
22600 SUM=0
22700 TOT=0
22800 G.TOT=0
22900 SUM$="0101"
23000 FOR S=1 TO Q
23100 IF (MID$(REC$(S),2,6)="ZZZZZZ") OR (VAL(MID$(REC$(S),57,8))=0) THEN             REC$(S)=STRING$(64,32):LPRINT REC$(S):GOTO 24200
23200 CODE$=MID$(REC$(S),8,4)
23300 IF SUM$<>CODE$ THEN LPRINT TAB(51)"TOTAL";TAB(60)USING "######,.##";TOT:        TOT=0:SUM$=CODE$:LINCNT=LINCNT+1
23400 LPRINT MID$(REC$(S),2,2)+"/"+MID$(REC$(S),4,2)+"/"+MID$(REC$(S),6,2);
23500 LPRINT TAB(12)MID$(REC$(S),8,2)+"/"+MID$(REC$(S),10,2);
23600 LPRINT TAB(20)MID$(REC$(S),12,45);
23700 LPRINT TAB(70)USING "######,.##";VAL(MID$(REC$(S),57,8))
23800 SUM=VAL(MID$(REC$(S),57,8))
23900 TOT=TOT+SUM
24000 G.TOT=G.TOT+SUM
24100 LINCNT=LINCNT+1:IF LINCNT=>58 THEN LPRINT CHR$(12):LINCNT=0 ELSE                GOTO 24200
24200 NEXT S
24300 LPRINT:LPRINT TAB(55)"GRAND TOTAL";TAB(70)USING "######,.##";G.TOT
24400 LPRINT CHR$(12)
24500 RETURN
24600 '
24700 '>>>EXIT ROUTINE<<<
24800 '
24900 CLOSE:KEY ON:CLS
```

## TMSTRT.BAS

```bas
10 '***************************************************************************
20 '*                             TMSTRT.BAS                                  *
30 '***************************************************************************
40 '*   This program, for use in a BATCH file, writes START TIME to the file, *
50 '* "TMSTRT.DTA", for use with the program, PRGTIM.DTA, in a BATCH file.    *
60 '*                                                                         *
70 '*           Program written by Steve Kent, CIS # 72406,504                *
80 '*                          v 1.1      05/03/84                            *
90 '***************************************************************************
100 H$=MID$(TIME$,1,2) : M$=MID$(TIME$,4,2) :DAT$=DATE$
110 STARTTIME=VAL(H$)*60 + VAL(M$) : TMHR=VAL(H$)
120 STARTTIME$="START= " + H$ + ":" + M$
130 IF TMHR < 12 THEN APM$="A.M."
140 IF TMHR = 12 AND M$ ="00" THEN APM$="NOON"
150 IF TMHR = 12 AND M$<>"00" THEN APM$="P.M."
160 IF TMHR > 12 THEN APM$="P.M." : TMHR=VAL(H$)-12
170 TMHR$=MID$(STR$(TMHR),2,2)
180 LOCATE 6,32 : PRINT "DATE: ";DAT$
190 LOCATE 10,1 :V$="WRITING START TIME":PRINT SPACE$((80-LEN(V$))*.5)+V$
200 LOCATE 12,1: V$="START TIME= " :                                                PRINT SPACE$((78-LEN(V$+M$+APM$+TMHR$))*.5)+V$+TMHR$+":"+M$+" "+APM$
210 OPEN "TMSTRT.DTA" FOR OUTPUT AS #1
220 WRITE #1, DAT$, STARTTIME$, STARTTIME
230 CLS : SYSTEM
```

{% comment %}samples_end{% endcomment %}

### Directory of PC-SIG Library Disk #0313

     Volume in drive A has no label
     Directory of A:\

    ACRS     BAS      3344   2-25-84   8:57p
    ACRS     DOC        96   2-25-84   7:22p
    CALCULA  BAS      5284   2-25-84   8:56p
    IPM      COM     31975  11-07-86  12:08a
    IPM      DOC     13064  11-07-86  12:20a
    KEITH2   CPM      1686  11-06-86  11:57p
    KEYPAD   BAS      1255   2-25-84   8:16p
    PCYEARBK DOC      9222   4-24-84   6:47p
    PCYEARBK EXE     26624   6-26-84  10:38p
    PERT3    BAS      9722   6-25-83  12:56p
    PRGTIM   BAS      6049   7-17-84   8:08p
    PRGTIM   DOC      2709   5-06-84   3:05p
    PRGTIM   EXE     23882   6-09-84   7:43p
    RPTUSE   BAS      4638   5-05-84  10:21p
    TAXDEDCT BAS      8969   2-25-84   7:50p
    TMSTRT   BAS      1465   5-03-84   7:58p
    TMSTRT   EXE     19200   5-05-84  12:12a
    USE-TIME DOC      3001   5-30-84   9:00p
    USERLOG  COM     25600   1-14-85  10:36p
    USERLOG  DOC     25108   1-14-85   6:46p
    FILES313 TXT      1233   1-30-87  12:52p
    GO       BAT       777   1-30-87   8:51a
           22 file(s)     224903 bytes
                           87040 bytes free
